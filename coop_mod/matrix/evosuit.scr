//-----------------------------------------------------------------------------------
// Script By:	Christian Sebastian Strahl (Chrissstrahl)
// Created on:	2023.01.14
// E-Mail:		chrissstrahl@yahoo.de
//-----------------------------------------------------------------------------------
// This script handles script based evosuit/spacewalk stuff
//-----------------------------------------------------------------------------------

void	coop_evosuit();
void	coop_evosuit_pickup(entity eTrigger,entity ePlayer);
float	coop_evosuit_playerPickedUp(entity ePlayer);
void	coop_evosuit_playerOn(entity ePlayer,float fPickedUp);
void	coop_evosuit_playerOff(entity ePlayer);
void	coop_evosuit_playerSufficate(entity ePlayer);
float	coop_evosuit_playerEquiped(entity ePlayer);
float	coop_evosuit_spaceWorthy(entity eSentity);
void	coop_evosuit_airlockCompress(entity eAirLock);
void	coop_evosuit_airlockDecompress(entity eAirLock);
void	coop_evosuit_playerDecompress(entity ePlayer);
void	coop_evosuit_playerSetSpaceGravity(entity ePlayer,float fYes);
void	coop_evosuit_playerSetSpace(entity ePlayer,float fSpace);
float	coop_evosuit_playerInSpace(entity ePlayer);
void	coop_evosuit_airlockInside();
void	coop_evosuit_airlockOutside();
void	coop_evosuit_haltStat(entity ePlayer,float fHalt);
void	coop_evosuit_attachSuit(entity ePlayer);
void	coop_evosuit_deattachSuit(entity ePlayer);
void	coop_evosuit_justTransported(entity ePlayer);
void	coop_evosuit_justBecameSpectator(entity ePlayer);
void	coop_evosuit_classChanged(entity ePlayer);
void	coop_evosuit_modelChanged(entity ePlayer);
void	coop_evosuit_3rdPerson(entity ePlayer);
float	coop_evosuit_helmetHud(entity ePlayer);
void	coop_evosuit_setHudHelmet(entity ePlayer, float fOn);
void	coop_evosuit_setHudAirsupply(entity ePlayer, float fOn);
void	coop_evosuit_setHudThruster(entity ePlayer, float fOn);
void	coop_evosuit_setHudIceing(entity ePlayer, float fOn);

//these are global for this level script using __ as prefix so highligther shows them
//USE THEM IN YOUR SCRIPT FOR CONFIGURATION, SO YOU DONT HAVE TO EDIT THE EVOSUIT SCRIPT
float		__EVOSUIT_INT_EXO_FREEZE_TIME			= 5; //time until stalker player freezes in space for not using thrusters
float		__EVOSUIT_FLT_SPACE_GRAVITY				= 0.15;
float		__EVOSUIT_INT_SUFFICATE_HURT			= 10;
float		__EVOSUIT_INT_SUFFICATE_WAIT			= 5;
float		__EVOSUIT_INT_THRUSTER_COOLDOWN_TIME	= 10;
float		__EVOSUIT_INT_THRUSTER_EMERGENCY_COST	= 40;
float		__EVOSUIT_INT_THRUSTER_MOVE_COST		= 0.5;
float		__EVOSUIT_INT_THRUSTER_MOVE_UP_COST		= 1.5;
float		__EVOSUIT_INT_THRUSTER_MOVE_DOWN_COST	= 1;
float		__EVOSUIT_INT_THRUSTER_MOVE_FORCE		= 50;
vector		__EVOSUIT_VEC_AIRLOCK_DECOMPRESS_PUSH	= '0 0 0'; //x y z - can also be negative for each
float		__EVOSUIT_FLT_AIRSUPPLY_CONSUMPTION		= 0.025;
string		__EVOSUIT_STR_HUD_AIRSUPPLY				= "cooptimerblue";
string		__EVOSUIT_STR_HUD_AIRSUPPLY_WARNING		= "coop_evosuitair";
string		__EVOSUIT_STR_HUD_AIRSUPPLY_CVAR		= "coop_timerBlue";
string		__EVOSUIT_STR_HUD_AIRSUPPLY_STAT		= "mp_generic1";
string		__EVOSUIT_STR_HUD_THRUSTER				= "cooptimerred";
string		__EVOSUIT_STR_HUD_THRUSTER_WARNING		= "coop_evosuitthrusters";
string		__EVOSUIT_STR_HUD_THRUSTER_CVAR			= "coop_timerRed";
string		__EVOSUIT_STR_HUD_THRUSTER_STAT			= "generic";
string		__EVOSUIT_STR_HUD_ICEING				= "coop_iceing";
string		__EVOSUIT_STR_HUD_HELMET				= "coop_evoHelmet";
string		__EVOSUIT_STR_HUD_THRUSTER_STRING		= "$$c#156$$";
string		__EVOSUIT_STR_HUD_AIRSUPPLY_STRING		= "$$AirSupply$$";

void coop_evosuit()
//------------------------------------------------------------------------------
//Equip or Unequip Evosuit for player using this trigger
//
//This is to be called from a trigger on the level
//------------------------------------------------------------------------------
{
	entity eTrigger,ePlayer;
	eTrigger = getCurrentEntity();
	if(doesEntityExist(eTrigger)){	
		ePlayer = eTrigger.getLastActivatingEntity();
		if(doesEntityExist(ePlayer)){
			//[b60025] chrissstrahl - updated to reduce warning message spam
			if(coop_evosuit_playerEquiped(ePlayer) == 1 && coop_evosuit_playerPickedUp(ePlayer) == 1){	
				thread coop_evosuit_playerOff(ePlayer);
				
			}else{
				thread coop_evosuit_playerOn(ePlayer,1);
				thread coop_evosuit_pickup(eTrigger,ePlayer);
			}
		}
	}
}

void coop_evosuit_pickup(entity eTrigger,entity ePlayer)
//------------------------------------------------------------------------------
//picks up a evo suit for player - grab all of same targetname
//------------------------------------------------------------------------------
{
	if(eTrigger.getFloatVar("coop_evosuit_taken") == 1){
		return;
	}
	
	eTrigger.setFloatVar("coop_evosuit_taken",1);
	
	entity eSuit;
	eSuit = eTrigger.getTargetEntity();
	while(doesEntityExist(eSuit)){
		eSuit.hide();
		
		eSuit = getNextEntity(eSuit);
	}
	
	wait(2);
	
	eSuit = eTrigger.getTargetEntity();
	while(doesEntityExist(eSuit)){
		eSuit.show();
		eSuit.displayEffect("TransportIn","FederationWeapons");
		
		eSuit = getNextEntity(eSuit);
	}
	
	eTrigger.setFloatVar("coop_evosuit_taken",0);
}


void coop_evosuit_playerOn(entity ePlayer,float fPickedUp)
//------------------------------------------------------------------------------
//put on evosuit
//------------------------------------------------------------------------------
{
	float fHealth;
	float fAirSupplyLastHurt;
	float iAirSupply = 100;
	float iLastAirSupply;
	float iLastAirSupplyWarning;
	float iThrusterWarning;
	float bAirSupplyStopSound=1;
	vector vVelocity,vVelocityNew; 
	
	float iStatVal = -1;
	float iLastStatVal = 123;
	float iCoolDownTimmer = 0;
	float iCoolDownTime;
	float iUpCost = 1.5;
	float fRecharge = -0.5;
	
	float iTrusersInactiveTime;
	float iLastFreezeHurt;
	float iFreezing;
	string sPlayerModel;
	
	sPlayerModel	= ePlayer.getModelName();
	
	iTrusersInactiveTime  = ((1 / coop_float_oneFrameTime) * __EVOSUIT_INT_EXO_FREEZE_TIME);	
	iCoolDownTime = ((1 / coop_float_oneFrameTime) * __EVOSUIT_INT_THRUSTER_COOLDOWN_TIME);
	
	//rename this thread so it can be killed from the outside
	string sKillThread;
	sKillThread = "coop_evosuitPlayerOnPlayer"+ePlayer.getEntNum();
	threadname(sKillThread);
	
	ePlayer.setFloatVar("coop_evosuit_on",1);
	if(fPickedUp){
		coop_evosuit_attachSuit(ePlayer);
		ePlayer.setFloatVar("coop_evosuit_pickedUp",1);
	}
	
	if(sPlayerModel != "models/char/dm_stalker.tik"){
		coop_evosuit_setHudAirsupply(ePlayer,1);
		if(__EVOSUIT_STR_HUD_AIRSUPPLY_CVAR != ""){
			thread globalCoop_player_transmitter(ePlayer,"set "+__EVOSUIT_STR_HUD_AIRSUPPLY_CVAR+" "+__EVOSUIT_STR_HUD_AIRSUPPLY_STRING);
		}
	}
	if(__EVOSUIT_STR_HUD_THRUSTER_CVAR != ""){
		thread globalCoop_player_transmitter(ePlayer,"set "+__EVOSUIT_STR_HUD_THRUSTER_CVAR+" "+__EVOSUIT_STR_HUD_THRUSTER_STRING);
	}
	
	while(doesEntityExist(ePlayer) == 1 && globalCoop_check_playerSpectator(ePlayer) == 0){
		//this is here for testing and needs to be removed laterz
		//if(ePlayer.checkReload()){	iAirSupply=100;	}
		
		fHealth			= ePlayer.getHealth();
		vVelocity		= ePlayer.getVelocity();
		vVelocityNew 	= vVelocity;
		
		//player dead, unsuit
		if(fHealth <= 0){
			ePlayer.setFloatVar("coop_respawnAtRespawnpoint",1);
			coop_evosuit_playerSetSpace(ePlayer,0);
			coop_evosuit_playerOff(ePlayer);
			return;
		}
		
		//sufficate
		if(sPlayerModel != "models/char/dm_stalker.tik"){
			if(iAirSupply <= 0){
				if(bAirSupplyStopSound){
					bAirSupplyStopSound = 0;
					ePlayer.stoploopsound();
					ePlayer.hudprint("You ran out of Air!\n");
				}
				
				if( (fAirSupplyLastHurt + __EVOSUIT_INT_SUFFICATE_WAIT) < getLevelTime()){
					fAirSupplyLastHurt = getLevelTime();
					thread coop_evosuit_playerSufficate(ePlayer);
				}
			}
			//use air
			else{
				if(iLastAirSupply < iAirSupply){ //on refil or start
					ePlayer.loopsound("sound/player/player_rebreath.wav",1,96);
					
					//AIRSUPPLY WARNING HUD RESET
					if(iLastAirSupplyWarning){
						iLastAirSupplyWarning = 0;
						ePlayer.removeHud(__EVOSUIT_STR_HUD_AIRSUPPLY_WARNING);
					}
				}
				
				iLastAirSupply = iAirSupply;
			
				fAirSupplyLastHurt = getLevelTime();
				iAirSupply = (iAirSupply - __EVOSUIT_FLT_AIRSUPPLY_CONSUMPTION);
				
				//AIRSUPPLY WARNING HUD - AT OR UNDER 15%
				if(iLastAirSupplyWarning == 0 && iAirSupply <= 15){
					//ePlayer.hudprint("airsupply\n");
					iLastAirSupplyWarning = 1;
					ePlayer.addHud(__EVOSUIT_STR_HUD_AIRSUPPLY_WARNING);
				}
			}
		}
		
		//thrusters
		if(ePlayer.isplayeronground() == 0 && iStatVal < 100 && iCoolDownTimmer == 0){
			if(coop_evosuit_playerInSpace(ePlayer)){
				if(ePlayer.checkJump()){
					if(vVelocity_z < 700){ //cap max upwards velocity for thrusters
						vVelocityNew_z += __EVOSUIT_INT_THRUSTER_MOVE_FORCE; 
						if(vVelocity_z < 0){
							iStatVal = (iStatVal + (__EVOSUIT_INT_THRUSTER_MOVE_UP_COST * 2) ); //it costs more to deaccelerate
						}else{
							iStatVal = (iStatVal + __EVOSUIT_INT_THRUSTER_MOVE_UP_COST);
						}
					}
				}
				else if(ePlayer.checkCrouch()){
					if(vVelocity_z > -400){ //cap max downwards velocity for thrusters
						if(vVelocity_z > 0 ){
							vVelocityNew_z -= __EVOSUIT_INT_THRUSTER_MOVE_FORCE;
							iStatVal = ( iStatVal + (__EVOSUIT_INT_THRUSTER_MOVE_DOWN_COST * 2) ); //it costs more to deaccelerate
						}else{
							vVelocityNew_z -= (__EVOSUIT_INT_THRUSTER_MOVE_FORCE / 1.5);
							iStatVal = (iStatVal + __EVOSUIT_INT_THRUSTER_MOVE_DOWN_COST);
						}
					}
				}
				
				if(ePlayer.checkForward()){
					iStatVal = (iStatVal + __EVOSUIT_INT_THRUSTER_MOVE_COST);
				}else if(ePlayer.checkBackward()){
					iStatVal = (iStatVal + __EVOSUIT_INT_THRUSTER_MOVE_COST);
				}
				
				if(ePlayer.checkLeft()){
					iStatVal = (iStatVal + __EVOSUIT_INT_THRUSTER_MOVE_COST);
				}else if(ePlayer.checkRight()){
					iStatVal = (iStatVal + __EVOSUIT_INT_THRUSTER_MOVE_COST);
				}
			}
		}
		
		//set new velocity
		if(vVelocityNew != vVelocity){
			ePlayer.setVelocity(vVelocityNew);
		}
		
		//make sure we don't go out of bounds
		if(iStatVal > 100){ iStatVal = 100;	}
		else if(iStatVal < 0){ iStatVal = 0;}
		if(iAirSupply < 0){iAirSupply = 0;}
		else if(iAirSupply > 100){iAirSupply = 100;}
		
		//air supply
		if(iAirSupply != iLastAirSupply && sPlayerModel != "models/char/dm_stalker.tik"){
			ePlayer.setstat(__EVOSUIT_STR_HUD_AIRSUPPLY_STAT,iAirSupply);
		}
		
		//THRUSTER WARNING HUD - AT OR OVER 75%
		if(iThrusterWarning == 0){
			if(iStatVal >= 75){
				iThrusterWarning = 1;
				ePlayer.addHud(__EVOSUIT_STR_HUD_THRUSTER_WARNING);
			}
		}
		else{
			//THRUSTER WARNING HUD - REMOVE
			if(iStatVal <= 55 && iCoolDownTimmer <= 0){
				iThrusterWarning = 0;
				ePlayer.removeHud(__EVOSUIT_STR_HUD_THRUSTER_WARNING);
			}
		}
		
		//set cooldown timer
		if(iStatVal == 100){
			ePlayer.hudprint("Thrusters need to Cooldown!\n");
			iCoolDownTimmer = iCoolDownTime;
		}		

		//no thrusters used - recharge
		if(iLastStatVal == iStatVal){
			iStatVal = (iStatVal + fRecharge);			
			iCoolDownTimmer--;
			if(iCoolDownTimmer < 0){
				iCoolDownTimmer=0;
			}else if(iCoolDownTimmer==1){
				ePlayer.hudprint("Thrusters operational agaian!\n");
				
				//if player is falling - fire emergency stabilisation boosters - but they are expansive
				if(sPlayerModel != "models/char/dm_stalker.tik"){
					if(ePlayer.isplayeronground() == 0 && vVelocity_z < -300){ //player could be on a lift, so check for ground
						ePlayer.hudprint("== Emercency Stabilisation ==\n");
						vVelocityNew = '0 0 150';
						iStatVal = __EVOSUIT_INT_THRUSTER_EMERGENCY_COST;
						ePlayer.setVelocity(vVelocityNew);
					}
				}
			}
		}
		
		//stalker freeze if thrusters are cold
		if(coop_evosuit_playerInSpace(ePlayer)){
			if(iStatVal <= 0){
				if(sPlayerModel == "models/char/dm_stalker.tik"){
					iTrusersInactiveTime--;
					if(iTrusersInactiveTime <= 0){
						if(iFreezing == 0){
							iFreezing=1;
							iLastFreezeHurt = getLevelTime();
							coop_evosuit_setHudIceing(ePlayer,1);
						}
						if((iLastFreezeHurt + 5) < getLevelTime()){
							iLastFreezeHurt = getLevelTime();
							coop_evosuit_playerSufficate(ePlayer);
						}
					}
				}
			}else{
				if(iFreezing == 1){
					iFreezing=0;
					coop_evosuit_setHudIceing(ePlayer,0);
				}
				iTrusersInactiveTime  = ((1 / coop_float_oneFrameTime) * __EVOSUIT_INT_EXO_FREEZE_TIME);
			}
		}

		//thrusters
		//[b60025] chrissstrahl - updated to reduce warning message spam
		if(iStatVal != iLastStatVal){
			if(!ePlayer.doesVarExist("coop_evosuit_haltStat")){
				ePlayer.setFloatVar("coop_evosuit_haltStat",0.0);
			}		
		
			if(ePlayer.getFloatVar("coop_evosuit_haltStat") == 0){
				ePlayer.setstat(__EVOSUIT_STR_HUD_THRUSTER_STAT,iStatVal);
			
				if(iStatVal < 0){
					iStatVal=0;
				}
			
				//sync last and current
				iLastStatVal = iStatVal;
			}
		}
		
		globalCoop_main_waitAFrame();
	}
}

void coop_evosuit_playerOff(entity ePlayer)
//------------------------------------------------------------------------------
//remove/takeoff evosuit
//------------------------------------------------------------------------------
{
	//if(ePlayer.getFloatVar("coop_evosuit_on")){
		ePlayer.setFloatVar("coop_evosuit_on",0);
		
		ePlayer.setFloatVar("coop_evosuit_pickedUp",0);
		
		string sKillThread;
		sKillThread = "coop_evosuitPlayerOnPlayer"+ePlayer.getEntNum();
		killthread(sKillThread);
		
		ePlayer.stoploopsound();
		ePlayer.removehud(__EVOSUIT_STR_HUD_AIRSUPPLY_WARNING);
		ePlayer.removehud(__EVOSUIT_STR_HUD_THRUSTER_WARNING);
		coop_evosuit_setHudAirsupply(ePlayer,0);
		coop_evosuit_setHudThruster(ePlayer,0);
		
		//REMOVE HELMET HUD
		
		string sModelName;
		sModelName = ePlayer.getModelName();
		if(	getIntStringFind(sModelName,"dm_attrexian_security-male.tik") == -1 &&
			getIntStringFind(sModelName,"evosuit") == -1 &&
			getIntStringFind(sModelName,"eb_gonzales") == -1 &&
			getIntStringFind(sModelName,"eb_idryll") == -1 &&
			getIntStringFind(sModelName,"spacesuit") == -1 )
		{
			coop_evosuit_setHudHelmet(ePlayer,0);
		}
		//ePlayer.setFloatVar("coop_evosuit_3rdPersonRunning",1);
		
		coop_evosuit_deattachSuit(ePlayer);
	//}
}

void coop_evosuit_playerSufficate(entity ePlayer)
//------------------------------------------------------------------------------
//if player has the suit on but no airz, killz thez bitchz
//------------------------------------------------------------------------------
{
	$world.setStringVar("uservar1","killmessage  did ^2sufficate...");
	$world.setStringVar("uservar2","killmessage_de  ist ^2erstickt...");

	if(doesEntityExist(ePlayer)){
		float fHealth;
		fHealth = ePlayer.getHealth();
		
		if(fHealth < __EVOSUIT_INT_SUFFICATE_HURT){
			ePlayer.health(1);
			ePlayer.killed($world,99999, $world, 0 );
			coop_evosuit_playerOff(ePlayer);
			ePlayer.setFloatVar("coop_evosuit_inSpace",0);
			ePlayer.setFloatVar("coop_evosuit_on",0);
			ePlayer.setFloatVar("coop_evosuit_pickedUp",0);			
		}else{
			ePlayer.pain(1,$world,0);
			ePlayer.health(fHealth - __EVOSUIT_INT_SUFFICATE_HURT);
		}
	}
}

float coop_evosuit_playerEquiped(entity ePlayer)
//------------------------------------------------------------------------------
//check if equiped
//------------------------------------------------------------------------------
{
	//[b60025] chrissstrahl - updated to reduce warning message spam
	if(!ePlayer.doesVarExist("coop_evosuit_on")){
		ePlayer.setFloatVar("coop_evosuit_on",0);
	}
	
	if(ePlayer.getFloatVar("coop_evosuit_on") == 1){
		return 1;
	}
	return 0;
}

float coop_evosuit_playerPickedUp(entity ePlayer)
//------------------------------------------------------------------------------
//check if has picked up a suit
//------------------------------------------------------------------------------
{
	//[b60025] chrissstrahl - updated to reduce warning message spam
	if(!ePlayer.doesVarExist("coop_evosuit_pickedUp")){
		ePlayer.setFloatVar("coop_evosuit_pickedUp",0);
	}
	
	if(ePlayer.getFloatVar("coop_evosuit_pickedUp") == 1){
		return 1;
	}
	return 0;
}

float coop_evosuit_spaceWorthy(entity eSentity)
//------------------------------------------------------------------------------
//check if sentity is space worthy by model or evosuit
//------------------------------------------------------------------------------
{
	//check for models that validate for space usage
	string sModelName;
	sModelName = eSentity.getModelName();
	//[b60025] chrissstrahl - updated to reduce warning message spam
	if(	coop_evosuit_playerPickedUp(eSentity) == 1 ||
		getIntStringFind(sModelName,"dm_attrexian_security-male.tik") > -1 ||
		getIntStringFind(sModelName,"evosuit") > -1 ||
		getIntStringFind(sModelName,"eb_gonzales") > -1 ||
		getIntStringFind(sModelName,"eb_idryll") > -1 ||
		getIntStringFind(sModelName,"spacesuit") > -1 ||
		getIntStringFind(sModelName,"borg_") > -1 ||
		getIntStringFind(sModelName,"alien-type") > -1 ||
		getIntStringFind(sModelName,"_stalker") > -1 ||
		getIntStringFind(sModelName,"saboteur-gauntlet") > -1)
	{
		return 1;
	}
	return 0;
}

void coop_evosuit_playerSetSpaceGravity(entity ePlayer,float fYes)
//------------------------------------------------------------------------------
//sets space or no space gravity for player
//------------------------------------------------------------------------------
{
	if(doesEntityExist(ePlayer)){
		if(fYes == 1){
			//[b60025] chrissstrahl - added check so players that commit suicide will not stay in lowgrav
			if(ePlayer.getHealth() > 0){
				ePlayer.gravity(__EVOSUIT_FLT_SPACE_GRAVITY);
			}
		}
		else{
			string sClass;
			float fGravity;
			fGravity = 1;
			
			sClass = ePlayer.getCoopClass();
			if(sClass == "HeavyWeapons"){
				fGravity=1;
			}
			else if(sClass == "Medic"){
				fGravity=0.85;
			}
			else{ //"Technician"
				fGravity=0.9;
			}
			
			ePlayer.gravity(fGravity);
		}
	}
}

void coop_evosuit_playerSetSpace(entity ePlayer,float fSpace)
//------------------------------------------------------------------------------
//sets if player is in space or not
//------------------------------------------------------------------------------
{
	ePlayer.setFloatVar("coop_evosuit_inSpace",fSpace);	
	if(fSpace == 1){
		ePlayer.setFloatVar("coop_respawnAtRespawnpoint",1);
	
		thread coop_evosuit_playerDecompress(ePlayer);
		coop_evosuit_playerSetSpaceGravity(ePlayer,1);
		
		//player worthy of space even without picking suit up - auto put on
		if(coop_evosuit_spaceWorthy(ePlayer) == 1){
			coop_evosuit_setHudThruster(ePlayer,1);
			if(coop_evosuit_playerEquiped(ePlayer) == 0){		
				thread coop_evosuit_playerOn(ePlayer,0); //mark as not picked up
			}
		}
	}else{
		ePlayer.setFloatVar("coop_respawnAtRespawnpoint",0);
		
		//leaving space - and did not pick up suit but was spaceworthy, take off the auto evo suit
		//[b60025] chrissstrahl - updated to reduce warning message spam
		if(coop_evosuit_playerPickedUp(ePlayer) == 0 && coop_evosuit_playerEquiped(ePlayer) == 1){
			thread coop_evosuit_playerOff(ePlayer);
		}
		coop_evosuit_playerSetSpaceGravity(ePlayer,0);
		coop_evosuit_setHudThruster(ePlayer,0);
	}
}

float coop_evosuit_playerInSpace(entity ePlayer)
//------------------------------------------------------------------------------
//gets if player is in space or not
//------------------------------------------------------------------------------
{
	float fSpace;
	//[b60025] chrissstrahl - updated to reduce warning message spam
	if(!ePlayer.doesVarExist("coop_evosuit_inSpace")){
		ePlayer.setFloatVar("coop_evosuit_inSpace",0);
		return 0;
	}
	fSpace = ePlayer.getFloatVar("coop_evosuit_inSpace");
	return fSpace;
}

void coop_evosuit_playerDecompress(entity ePlayer)
//------------------------------------------------------------------------------
//scales player and then kills player
//------------------------------------------------------------------------------
{
	//[b60025] chrissstrahl - updated to reduce warning message spam
	if(!ePlayer.doesVarExist("coop_evosuit_decompressing")){
		ePlayer.setFloatVar("coop_evosuit_decompressing",0);
	}else{	
		if(ePlayer.getFloatVar("coop_evosuit_decompressing") == 1){
			return;
		}
	}
	
	ePlayer.setFloatVar("coop_evosuit_decompressing",1);
	
	if(coop_evosuit_spaceWorthy(ePlayer) == 1){
		wait(2);
		if(doesEntityExist(ePlayer)){
			coop_evosuit_setHudIceing(ePlayer,0);
			ePlayer.setFloatVar("coop_evosuit_decompressing",0);
		}
		return;
	}
	
	
	coop_evosuit_setHudIceing(ePlayer,1);
	ePlayer.setCustomShader("coop_iceing");
	//[b60025] chrissstrahl - added check so players that commit suicide will not stay in lowgrav
	if(ePlayer.getHealth() > 0){
		ePlayer.gravity(0);
		ePlayer.setmovetype("bounce");
	}
	
	//ePlayer.immobilize(1);
	
	//make sure player respawns at spawn spot info_player_deathmatch
	ePlayer.setFloatVar("coop_respawnAtRespawnpoint",1);

	if(doesEntityExist(ePlayer)){
		//[b60025] chrissstrahl - added check so players that commit suicide will not stay in lowgrav
		float fTime;
		while(doesEntityExist(ePlayer) == 1 && fTime < 2){
			if(ePlayer.getHealth() > 0){
				globalCoop_main_waitAFrame();
				fTime += coop_float_oneFrameTime;
			}else{
				fTime = 2;
			}
		}

		if(doesEntityExist(ePlayer) == 1 && ePlayer.getHealth() > 0){
			ePlayer.kill();
		}
		
		if(doesEntityExist(ePlayer)){
			thread coop_evosuit_playerSetSpace(ePlayer,0);
			thread coop_evosuit_playerOff(ePlayer);
			
			ePlayer.setmovetype("walk");
			ePlayer.gravity(1);
			coop_evosuit_setHudIceing(ePlayer,0);
			ePlayer.setFloatVar("coop_evosuit_decompressing",0);
		}
	}
}

void coop_evosuit_airlockCompress(entity eAirLock)
//------------------------------------------------------------------------------
//Handles decompressing of airlock
//------------------------------------------------------------------------------
{
	if(!doesEntityExist(eAirLock)){
		print("coop_evosuitAirlockCompress() - Airlock Inside Trigger is missing or not specified!\n");
		centerprint("coop_evosuitAirlockCompress() - Airlock Inside Trigger is missing or not specified!\n");
		return;
	}
	
	eAirLock.setFloatVar("coop_evosuit_AirLockDecompressed",0);
}

void coop_evosuit_airlockDecompress(entity eAirLock)
//------------------------------------------------------------------------------
//Handles decompressing of airlock
//------------------------------------------------------------------------------
{
	if(!doesEntityExist(eAirLock)){
		print("coop_evosuitAirlockDecompress() - Airlock Inside Trigger is missing or not specified!\n");
		centerprint("coop_evosuitAirlockDecompress() - Airlock Inside Trigger is missing or not specified!\n");
		return;
	}

	entity ePlayer;
	float fPlayerIdInUse;
	vector vVel;
	
	eAirLock.setFloatVar("coop_evosuit_AirLockDecompressed",1);
	
	//add icing hud, push players up and away decompress area
	//manage actors that wandered into airlock
	//take care of actors and players inside
	float fMaxEnt;
	fMaxEnt = getCvarInt("maxentities");
	for(fPlayerIdInUse=0;fPlayerIdInUse<fMaxEnt;fPlayerIdInUse++){
		//[b60025] Chrissstrahl - using now entity num instead of targetname
		ePlayer = getEntity("*"+fPlayerIdInUse);
		if(doesEntityExist(ePlayer)){
			//[b60025] chrissstrahl - updated to fix null ent warning
			if(ePlayer.getHealth() > 0){
				//check all players touching this trigger
				if(ePlayer.isEntityInsideOfEntity(eAirLock) == 1){
					coop_evosuit_playerSetSpace(ePlayer,1);
					if(ePlayer.getSubclassName() == "Player"){
						//make sure player respawns at spawn spot info_player_deathmatch
						ePlayer.setFloatVar("coop_respawnAtRespawnpoint",1);
					
						//kill players that are inside without evo 
						thread coop_evosuit_playerDecompress(ePlayer);
						
						vVel = ePlayer.getVelocity();
						vVel = (vVel + __EVOSUIT_VEC_AIRLOCK_DECOMPRESS_PUSH);
						ePlayer.setvelocity(vVel);
						
						coop_evosuit_setHudIceing(ePlayer,1);
					}else if(ePlayer.getSubclassName() == "Actor"){
						if(!coop_evosuit_spaceWorthy(ePlayer)){
							//ePlayer.immobilize(1);
							ePlayer.gravity(0);
							ePlayer.suicide();
							ePlayer.setCustomShader("coop_iceing");
						}						
					}
				}
				//BUGSPRAY
				//else{
					//print("coop_evosuit_airlockDecompress ("+ePlayer.getTargetname()+")NOT-INSIDE: "+eAirLock.getTargetname()+"\n");
				//}
			}
		}
	}
}

void coop_evosuit_airlockInside()
//------------------------------------------------------------------------------
// called if a player is inside the airlock trigger area
//------------------------------------------------------------------------------
{
	entity eTrigger,ePlayer;
	eTrigger = getCurrentEntity();
	
	if(doesEntityExist(eTrigger)){
		float fPlayerIdInUse;
		for(fPlayerIdInUse=0;fPlayerIdInUse<coop_integer_maxPlayers;fPlayerIdInUse++){
			ePlayer = getEntity("*"+fPlayerIdInUse);
			if(doesEntityExist(ePlayer)){
				//check all players touching this trigger
				if(ePlayer.isEntityInsideOfEntity(eTrigger) == 1){
					//[b60025] chrissstrahl - updated to reduce warning message spam
					if(!eTrigger.doesVarExist("coop_evosuit_AirLockDecompressed")){
						eTrigger.setFloatVar("coop_evosuit_AirLockDecompressed",0.0);
					}
				
					if(eTrigger.getFloatVar("coop_evosuit_AirLockDecompressed") == 0){
						coop_evosuit_playerSetSpace(ePlayer,0);
					}else{
						coop_evosuit_playerSetSpace(ePlayer,1);
					}
				}
			}
		}	
	}
}

void coop_evosuit_airlockOutside()
//------------------------------------------------------------------------------
// called if a player is outside the airlock, which means in space
//------------------------------------------------------------------------------
{
	entity eTrigger,ePlayer,eActivator;
	eTrigger = getCurrentEntity();
	if(doesEntityExist(eTrigger)){
		eActivator = eTrigger.getLastActivatingEntity();
		if(eActivator.getSubclassName() == "Actor"){
			if(!coop_evosuit_spaceWorthy(eActivator)){
				eActivator.gravity(0);
				eActivator.suicide();
				eActivator.setCustomShader("coop_iceing");
			}
		}
	}
	
	if(doesEntityExist(eTrigger)){
		float fPlayerIdInUse;
		for(fPlayerIdInUse=0;fPlayerIdInUse<coop_integer_maxPlayers;fPlayerIdInUse++){
			//[b60025] Chrissstrahl - using now entity num instead of targetname
			ePlayer = getEntity("*"+fPlayerIdInUse);
			if(doesEntityExist(ePlayer)){
				//check all players touching this trigger
				if(ePlayer.isEntityInsideOfEntity(eTrigger)){
					coop_evosuit_playerSetSpace(ePlayer,1);
				}
			}
		}
	}
}

void coop_evosuit_haltStat(entity ePlayer,float fHalt)
//------------------------------------------------------------------------------
// halts statbar for evosuit thrusters - mean to be used if puzzles are done
//------------------------------------------------------------------------------
{
	if(doesEntityExist(ePlayer)){
		if(fHalt != 1){
			fHalt = 0;
			coop_evosuit_setHudThruster(ePlayer,1);
		}else{
			coop_evosuit_setHudThruster(ePlayer,0);
		}
		ePlayer.setFloatVar("coop_evosuit_haltStat",fHalt);
	}
}

void coop_evosuit_attachSuit(entity ePlayer)
//------------------------------------------------------------------------------
// attaches evosuit model to player model
//------------------------------------------------------------------------------
{
	if(doesEntityExist(ePlayer)){
		if(!coop_evosuit_spaceWorthy(ePlayer)){
		
			if(!ePlayer.checkThirdperson()){
				thread coop_evosuit_helmetHud(ePlayer);
			}

			float fScale=1.15;
			float fScaleBooster=1;
			vector v1,v2,vBackPack1,vBackPack2;
			//vBackPack1 = ePlayer.getBackpackAttachOffset();
			vBackPack2 = ePlayer.getBackpackAttachAngles();
			v1='-3 0 10';
			v2='0 0 180';
			vBackPack1 = '5 0 0';
			string sPlayerModel;
			sPlayerModel=ePlayer.getModelName();
			if(	sPlayerModel == "models/char/dm_munro.tik" ||
				sPlayerModel == "models/char/munro.tik" ||
				sPlayerModel == "models/char/sf_munro.tik"||
				sPlayerModel == "models/char/dm_chell.tik" ||
				sPlayerModel == "models/char/dm_chang.tik" ||
				sPlayerModel == "models/char/dm_barclay.tik" ||
				sPlayerModel == "models/char/dm_drull_krindo.tik" ||
				sPlayerModel == "models/char/dm_tuvok.tik" ||
				sPlayerModel == "models/char/dm_picard.tik")
			{
				v1='-3 0 11';
				fScale=1.1;
				fScaleBooster = 1.0;
				
				if(sPlayerModel == "models/char/dm_chang.tik"){
					vBackPack1 = '6 0 0';
				}
			}
			else if(sPlayerModel == "models/char/dm_klingon_merc-female.tik"){
				v1='-3 0 11';
				fScale=1.3;
			}
			else if(	sPlayerModel == "models/char/dm_kourban.tik" ||
						sPlayerModel == "models/char/dm_attrexian_command-female.tik")
			{
				v1='-5 0 12';
				fScale=1.2;
				fScaleBooster=1.1;
				if(sPlayerModel == "models/char/dm_kourban.tik"){
					vBackPack1 = '6.5 0 0';
				}
			}
			else if(sPlayerModel == "models/char/dm_nausicaan_male-merc.tik")
			{
				v1='-2 -.5 13';
				fScale=1.45;
			}
			else if(	sPlayerModel == "models/char/dm_klingon_merc-boss.tik" ||
						sPlayerModel == "models/char/dm_nausicaan_male-merc.tik")
			{
				v1='-5 -.5 13';
				fScale=1.65;
			}
			else if(	sPlayerModel == "models/char/gbs_dm_illia.tik" ||
						sPlayerModel == "models/char/dm_jurot.tik" ||
						sPlayerModel == "models/char/dm_drull_Kleeya.tik" ||
						sPlayerModel == "models/char/dm_romulan_informant-boss.tik" ||
						sPlayerModel == "models/char/dm_romulan_stx-female.tik" ||
						sPlayerModel == "models/char/dm_telsia.tik" )
			{
				v1='-3 0 10';
				fScale=1;
				fScaleBooster=0.9;
			}
			else if(	sPlayerModel == "models/char/dm_andorian_merc-male.tik" ||
						sPlayerModel == "models/char/dm_romulan_rebel-commander.tik")
			{
				v1='-3 0 9';
				fScale=1.15;
			}

			else if(	sPlayerModel == "models/char/dm_ferengi_oolpax.tik")
			{
				v1='-1 0 10';
				fScale=1.3;
			}
			
			//HELMET MODEL
			//Bip01 Head
			//tag_head
			ePlayer.attachmodel("models/enviro/enterprise_clothing_evo_helmet.tik","tag_head",fScale,"evo-helmet"+ePlayer.getEntNum(),0,-1,-1,-1,-1,v1,v2);
			//BOOSTERPACK MODEL
			//Bip01 spine1
			//Bip01 spine2
			ePlayer.attachmodel("models/enviro/enterprise_clothing_evo_suit.tik","Bip01 spine1",fScaleBooster,"evo-booster"+ePlayer.getEntNum(),0,-1,-1,-1,-1,vBackPack1,vBackPack2);
		/*attachEvent->AddString( attachModelName.c_str() );
		attachEvent->AddString( tagName );
		attachEvent->AddFloat( 1.0f );
		attachEvent->AddString( "attachedSpecialityBackpack" );
		attachEvent->AddInteger( 0 );
		attachEvent->AddFloat( -1.0f );
		attachEvent->AddFloat( 0.0f );
		attachEvent->AddFloat( -1.0f );
		attachEvent->AddFloat( -1.0f );
		attachEvent->AddVector( player->getBackpackAttachOffset() );
		attachEvent->AddVector( player->getBackpackAttachAngles() );
		*/
		}
	}
}

void coop_evosuit_deattachSuit(entity ePlayer)
//------------------------------------------------------------------------------
// attaches evosuit model to player model
//------------------------------------------------------------------------------
{
	if(doesEntityExist(ePlayer)){
		ePlayer.removeAttachedModelByTargetname("evo-helmet"+ePlayer.getEntNum());
		ePlayer.removeAttachedModelByTargetname("evo-booster"+ePlayer.getEntNum());
	}
}

float coop_evosuit_helmetHud(entity ePlayer)
//------------------------------------------------------------------------------
// adds helmet hud if needed
//------------------------------------------------------------------------------
{
	string sModelName;
	sModelName = ePlayer.getModelName();
	//[b60025] chrissstrahl - updated to reduce warning message spam
	if(	coop_evosuit_playerPickedUp(ePlayer) == 0 && coop_evosuit_spaceWorthy(ePlayer) == 0 ||
		getIntStringFind(sModelName,"dm_attrexian_security-male.tik") > -1 ||
		//getIntStringFind(sModelName,"eb_gonzales") > -1 ||
		//getIntStringFind(sModelName,"eb_idryll") > -1 ||
		getIntStringFind(sModelName,"borg_") > -1 ||
		getIntStringFind(sModelName,"_stalker") > -1)
	{
		return 0;
	}
	coop_evosuit_setHudHelmet(ePlayer,1);
	return 1;
}

void coop_evosuit_3rdPerson(entity ePlayer)
//------------------------------------------------------------------------------
// takes care of 3rd person view - cg_3rd_person
//------------------------------------------------------------------------------
{
	if(ePlayer.getFloatVar("coop_evosuit_3rdPersonRunning") != 0){
		return;
	}
	ePlayer.setFloatVar("coop_evosuit_3rdPersonRunning",1);
	
	while(doesEntityExist(ePlayer)){
		if(ePlayer.checkThirdperson()){
			if(ePlayer.getFloatVar("coop_evosuit_3rdPersonRunning") != 2){
				ePlayer.setFloatVar("coop_evosuit_3rdPersonRunning",2);
				coop_evosuit_setHudHelmet(ePlayer,0);
			}
		}else{
			if(ePlayer.getFloatVar("coop_evosuit_3rdPersonRunning") != 3){
				if(coop_evosuit_helmetHud(ePlayer)){
					ePlayer.setFloatVar("coop_evosuit_3rdPersonRunning",3);
				}
			}
		}
		globalCoop_main_waitAFrame();
	}
}

//[b60025] chrissstrahl - make sure players that transport will be deald with if they transport into space without evosuit
void coop_evosuit_justTransported(entity ePlayer)
//------------------------------------------------------------------------------
// checks if player that was transported does have a evosuit on - needs to be called from: coop_justBecameSpectator() in main level script
//------------------------------------------------------------------------------
{
	if(doesEntityExist(ePlayer)){
		//Check to which target-player he transported
		//check if target-player is in space
		entity ePlayerClosest;
		ePlayerClosest = globalCoop_return_playerClosest(	ePlayer,			//The Origin Entity of the distance calculation
										0,		//The Minimum distance required to be between both entitys, 0=none
										15,		//The Maximum distance allowed between both entitys, 0=none
										0,		//Calculate based on the watch direction of the given entity (works only with players), 0=no
										1,			//Only Active Players
										"",	//name of the class to exclude
										0,//do not exclude but include the given class
										0);	//Check only two dimensional
		if(doesEntityExist(ePlayerClosest)){
			//[b60025] chrissstrahl - same player
			if(ePlayerClosest.getEntNum() == ePlayer.getEntNum()){
				return;
			}
		
			//target-player not in space, no further checks needed
			if(!coop_evosuit_playerInSpace(ePlayerClosest)){
				return;
			}
		}
		
		//set in space, the function will handle the suffication
		coop_evosuit_playerSetSpace(ePlayer,1);
		return;
	}
}

void coop_evosuit_justBecameSpectator(entity ePlayer)
//------------------------------------------------------------------------------
// removes evosuit model from player model - needs to be called from: coop_justBecameSpectator() in main level script
//------------------------------------------------------------------------------
{
	if(doesEntityExist(ePlayer)){
		ePlayer.setFloatVar("coop_respawnAtRespawnpoint",1);
		coop_evosuit_playerSetSpace(ePlayer,0);
		coop_evosuit_playerOff(ePlayer);
		if(doesEntityExist(ePlayer)){
			ePlayer.stoploopsound();
			ePlayer.setFloatVar("coop_evosuit_on",0);
			ePlayer.setFloatVar("coop_evosuit_pickedUp",0);
			//ePlayer.removeHud(__EVOSUIT_STR_HUD_HELMET);
			//ePlayer.removehud(__EVOSUIT_STR_HUD_AIRSUPPLY);
			//ePlayer.removehud(__EVOSUIT_STR_HUD_THRUSTER);
			coop_evosuit_setHudIceing(ePlayer,0);
		}
	}
}
void coop_evosuit_classChanged(entity ePlayer)
//------------------------------------------------------------------------------
// reattaches evosuit model to player model - needs to be called from: coop_justChangedClass() in main level script
//------------------------------------------------------------------------------
{
	if(doesEntityExist(ePlayer)){
		float fInSpace;
		fInSpace = coop_evosuit_playerInSpace(ePlayer);
		coop_evosuit_playerSetSpaceGravity(ePlayer,fInSpace);
	}
}

void coop_evosuit_modelChanged(entity ePlayer)
//------------------------------------------------------------------------------
// attaches evosuit model to player model - needs to be called from: coop_justChangedModel() in main level script
//------------------------------------------------------------------------------
{
	if(doesEntityExist(ePlayer)){
		float fInSpace;
		//float fSuitOn;
		float fPickedUp;
		fInSpace = coop_evosuit_playerInSpace(ePlayer);
		//[b60025] chrissstrahl - updated to reduce warning message spam
		//fSuitOn = coop_evosuit_playerEquiped(ePlayer);
		fPickedUp = coop_evosuit_playerPickedUp(ePlayer);

		//[b60025] chrissstrahl - updated to fix hud staying on when changing models from gonzales to other
		coop_evosuit_setHudHelmet(ePlayer,0);
		
		if(ePlayer.getFloatVar("coop_evosuit_3rdPersonRunning") > 0){
			ePlayer.setFloatVar("coop_evosuit_3rdPersonRunning",1);
		}
		
		if(fPickedUp){
			coop_evosuit_deattachSuit(ePlayer);
			ePlayer.setFloatVar("coop_evosuit_pickedUp",0);
			
			globalCoop_main_waitAFrame();
			if(doesEntityExist(ePlayer)){
				coop_evosuit_attachSuit(ePlayer);
				ePlayer.setFloatVar("coop_evosuit_pickedUp",1);
			}
		}
		//[b60025] chrissstrahl - fixed changing models from gonzales to any other while in space 
		else{
			if(fInSpace){
				thread coop_evosuit_playerDecompress(ePlayer);
				coop_evosuit_playerOff(ePlayer);
			}
		}
	}
}

void coop_evosuit_justEntered(entity ePlayer)
//------------------------------------------------------------------------------
// player just entered level - needs to be called from: coop_justEntered() in main level script
//------------------------------------------------------------------------------
{
	if(doesEntityExist(ePlayer)){
		ePlayer.setFloatVar("coop_evosuit_pickedUp",0); //[b60018] Chrissstrahl - stop variable does not exist warning spam
		thread coop_evosuit_3rdPerson(ePlayer);
	}
}

void coop_evosuit_setHudHelmet(entity ePlayer, float fOn)
//------------------------------------------------------------------------------
// [b60025] chrissstrahl - allow to add and remove huds
//------------------------------------------------------------------------------
{
	if(doesEntityExist(ePlayer)){
		if(fOn){
			ePlayer.addHud(__EVOSUIT_STR_HUD_HELMET);
		}else{
			ePlayer.removeHud(__EVOSUIT_STR_HUD_HELMET);
		}
	}
}

void coop_evosuit_setHudAirsupply(entity ePlayer, float fOn)
//------------------------------------------------------------------------------
// [b60025] chrissstrahl - allow to add and remove huds
//------------------------------------------------------------------------------
{
	if(doesEntityExist(ePlayer)){
		if(fOn){
			ePlayer.addHud(__EVOSUIT_STR_HUD_AIRSUPPLY);
		}else{
			ePlayer.removeHud(__EVOSUIT_STR_HUD_AIRSUPPLY);
		}
	}
}


void coop_evosuit_setHudThruster(entity ePlayer, float fOn)
//------------------------------------------------------------------------------
// [b60025] chrissstrahl - allow to add and remove huds
//------------------------------------------------------------------------------
{
	if(doesEntityExist(ePlayer)){
		if(fOn){
			ePlayer.addHud(__EVOSUIT_STR_HUD_THRUSTER);
		}else{
			ePlayer.removeHud(__EVOSUIT_STR_HUD_THRUSTER);
		}
	}
}

void coop_evosuit_setHudIceing(entity ePlayer, float fOn)
//------------------------------------------------------------------------------
// [b60025] chrissstrahl - allow to add and remove huds
//------------------------------------------------------------------------------
{
	if(doesEntityExist(ePlayer)){
		if(fOn){
			ePlayer.addHud(__EVOSUIT_STR_HUD_ICEING);
		}else{
			ePlayer.removeHud(__EVOSUIT_STR_HUD_ICEING);
		}
	}
}
