// Script/Cameras by Jared Hefty

void cinematicEntIntroIGM2 ();
void cinematicEntIntroIGM2End ();

void cinematicEntIntroIGM4 ();
void cinematicEntIntroIGM4End ();

void cinematicEntInterludeIGM4 ();
void cinematicEntInterludeIGM4End ();

void cinematicEntInterludeIGM4b ();
void cinematicEntInterludeIGM4bEnd ();

void cinematicEntIntroIGM7 ();
void cinematicEntIntroIGM7End ();

void initCinematicEntIntro( float IGM );
void initCinematicEntInterlude ( float interlude );


vector vectorStash;
float IGMCinematicDone = 0;

void Intro2 ()
{
	IGMCinematicDone = 0;

	initCinematicEntIntro( 2 );
	thread cinematicEntIntroIGM2 ();

	while ( !IGMCinematicDone )
	{
		wait ( .25 );
	}
}

void Intro4 ()
{
	IGMCinematicDone = 0;

	initCinematicEntIntro ( 4 );
	thread cinematicEntIntroIGM4 ();
	while ( !IGMCinematicDone )
	{
		wait ( .25 );
	}
}

void Intro7 ()
{
	IGMCinematicDone = 0;

	initCinematicEntIntro ( 7 );
	thread cinematicEntIntroIGM7 ();
	while ( !IGMCinematicDone )
	{
		wait ( .25 );
	}
	$entSkyOrigin.origin( $sky.getorigin() );
}

void Interlude4 ()
{
	IGMCinematicDone = 0;

	initCinematicEntInterlude ( 1 );
	thread cinematicEntInterludeIGM4 ();
	while ( !IGMCinematicDone )
	{
		wait ( .25 );
	}
	$entSkyOrigin.rendereffects ( "-skyorigin" );
	$sky.rendereffects( "+skyorigin" );
}

void Interlude4b ()
{
	IGMCinematicDone = 0;

	initCinematicEntInterlude ( 2 );
	thread cinematicEntInterludeIGM4b ();
	while ( !IGMCinematicDone )
	{
		wait ( .25 );
	}
	$entSkyOrigin.rendereffects ( "-skyorigin" );
	$sky.rendereffects( "+skyorigin" );
}

void initCinematicEntInterlude ( float interlude )
{
	$entSaucer.bind   ( $entOrigin );
	$entBody.bind     ( $entOrigin );
	$entNacelles.bind ( $entOrigin );

	$sky.rendereffects( "-skyorigin" );
	$entSkyOrigin.rendereffects ( "+skyorigin" );

	if ( interlude == 1 )
	{
		spawn ( "sky_planet_m6-industrial_highres", "scale", "2.10", "origin" , "13959 -12037 1419" , "model" , "sky/sky_planet_industrial-high.tik" , "targetname" , "planet" );
		spawn ( "sky_planet_ribbon-left", "scale", "2.10", "origin" , "13686 -11914 1424", "model", "sky/sky_planet_ribbon-left.tik", "targetname", "planetTrails" );
		wait( .25 );
		$planetTrails.angle( 240 );
		//hzm coop mod chrissstrahl - reactivated, dono who did and why it was deactivated but we got a hall of mirrows effect that we need to fix
		spawn ( "sky_skybox_brecha-rift" , "scale" , "5" , "origin" , "15680.00 -12784.00 1024.00" , "model" , "sky/sky_brecha-rift.tik");

		wait ( .1 );

		$planet.scale ( 1.5 );
	}
	else if ( interlude == 2 )
	{
		spawn ( "vehicle_excelsior_dallas-dmg-back" , "model" , "vehicle/dallas-dmg2.tik" , "origin" , "13334.60 -13481.90 990.30" ,"targetname" , "dallasBody" );
		spawn ( "vehicle_excelsior_dallas-dmg-front" , "model" , "vehicle/dallas-dmg1.tik" , "origin" , "13334.00 -13481.50 992.50" , "targetname" , "dallasSaucer" );
		spawn ( "script_origin" , "origin" , "13335 -13483 990" , "targetname" , "dallasOrigin" );

		wait ( .1 );

		$dallasSaucer.scale ( .35 );
		$dallasBody.scale ( .35 );
		$dallasSaucer.bind ( $dallasOrigin );
		$dallasBody.bind ( $dallasOrigin );

		spawn ( "sky_skybox_brecha-rift" , "scale" , "5" , "origin" , "15680.00 -12784.00 1024.00" , "model" , "sky/sky_brecha-rift.tik");
	}
}

void initCinematicEntIntro ( float IGM )
{
	//align the saucer more precisely than we can in radiant
	$entSaucer.origin ( $entBody.getorigin ());
	$entNacelles.origin ( $entBody.getorigin ());

	//this stuff will always happens, regardless of IGM

	$entSaucer.bind   ( $entOrigin );
	$entBody.bind     ( $entOrigin );
	$entNacelles.bind ( $entOrigin );

	//spawn and move things around as appropriate
	//we spawn stuff to avoid precaching large amounts of unneeded textures

	wait ( .1 );

	if ( IGM == 2 )
	{
		$entSkyOrigin.rendereffects ( "+skyorigin" );

		$entOrigin.origin ( '13168 -13248 1008' );

		spawn ( "script_origin" , "origin" , "13296 -13460 960" , "targetname" , "dallasOrigin" , "angle" , "45" );
		spawn ( "vehicle_excelsior_dallas-dmg-front" , "targetname" , "dallasSaucer" , "origin" ,"13296.00 -13463.50 960.50" , "model" , "vehicle/dallas-dmg1.tik" );
		spawn ( "vehicle_excelsior_dallas-dmg-back" , "targetname" , "dallasBody" , "origin" , "13296.60 -13463.90 960.30" , "model" , "vehicle/dallas-dmg2.tik" );

		spawn ( "SFX_fire_plasma_wispy_electric-blue" , "origin" , "13342.00 -13430.00 970.00" , "model" , "fx/fx-fire-plasma-wispy-electric-blue.tik" , "targetname" , "sparks" );
		spawn ( "SFX_fire_plasma_wispy_electric-blue" , "origin" , "13260.00 -13468.00 975.00" , "model" , "fx/fx-fire-plasma-wispy-electric-blue.tik" , "targetname" , "sparks2" );
		spawn ( "SFX_fire_plasma_wispy_electric-blue" , "origin" , "13410.00 -13422.00 942.00" , "model" , "fx/fx-fire-plasma-wispy-electric-blue.tik" , "targetname" , "sparks3" );

		spawn ( "SFX_steam_directional-blend-faint" , "origin" , "13310 -13436.00 948.00" ,  "model" , "fx/fx-steam-directional-heavy.tik" , "targetname" , "steam2" );

		wait ( .1 );
		$dallasSaucer.scale ( 0.35 );
		$dallasBody.scale   ( 0.35 );
		$steam2.scale ( .05 );
		$steam2.angles ( '0 45 0' );
		$sparks.scale ( .05 );
		$sparks2.scale ( .1 );
		$sparks3.scale ( .05 );
		$sparks3.angles ( '0 45 0' );

		$dallasSaucer.bind ( $dallasOrigin );
		$dallasBody.bind ( $dallasOrigin );
		$sparks.bind ( $dallasOrigin );

		$entOrigin.time ( .1 );
		$entOrigin.moveWest ( 1024 );

		$dallasOrigin.time ( .1 );
		$dallasOrigin.rotateXUp ( 15 );
		$dallasOrigin.rotateYUp ( 10 );

		//backdrop
		spawn ( "sky_skybox_brecha-rift" , "scale" , "5" , "origin" , "15680.00 -12784.00 1024.00" , "model" , "sky/sky_brecha-rift.tik" );
	}
	else if ( IGM == 4 )
	{
		$entSkyOrigin.rendereffects ( "+skyorigin" );

		$entOrigin.origin ( '13088 -17476 936' );
		spawn ( "sky_planet_m4-mining_highres" , "model" , "sky/sky_colony-planet.tik" , "origin" , "13044.00 -16856.00 1320.00" , "targetname" , "miningPlanet" );
		wait ( .1 );
		$miningPlanet.angle ( 90.0 );
		$miningPlanet.scale ( .95 );

		$entOrigin.angles ( '0 0 -15' );

		$entOrigin.followpath ( $entOrbit_2 , "normalangles" );
		wait ( .5 );
		$entOrigin.endpath ();
		//backdrop
		spawn ( "sky_skybox_brecha-rift" , "scale" , "5" , "origin" , "15680.00 -17392.00 1024.00" , "model" , "sky/sky_brecha-rift.tik" );

	}
	else if ( IGM == 7 )
	{
		$sky.rendereffects( "-skyorigin" );
		$entSkyOrigin.rendereffects ( "+skyorigin" );

		$entOrigin.origin ( '12456 -12980 1392' );
		spawn ( "sky_k7_station-sky" , "model" , "sky/k7starbase.tik" , "targetname" , "starbase" , "origin" , "12910.00 -12116.00 1572.00" );
		spawn ( "script_origin" , "targetname" , "starbaseOrigin" , "origin" , "12910 -12114 1560" );


		wait ( .1 );
		$starbase.angle ( 270.0 );
		$starbase.scale ( 2.0 );

		$starbase.bind ( $starbaseOrigin );

		$starbaseOrigin.time ( 5 );
		$starbaseOrigin.rotatey ( 1 );

		//backdrop
		spawn ( "sky_skybox_stardome-black" , "model" , "sky/sky_stardome-black.tik" , "origin" , "15680.00 -12800.00 1024.00" , "angles" , "0 -130 89");
		wait ( .1 );
		//additional parameters
	}
}

void cinematicEntIntroIGM2 ()
{
	cinematic ();
	freezeplayer ();
	letterbox ( .1 );

	spawn ( "Camera" , "targetname" , "entIntroCam" );
	cam.load ( "igm2_EntIntro" );

	wait ( .1 );
	skipthread ( "cinematicEntIntroIGM2End" );

	$entIntroCam.follow ( $igm2_EntIntro );
	$entIntroCam.cut ();
	cuecamera ( $entIntroCam );

	wait ( 1 );
	cam_fadein( 1, 0, 0, 0, 1, 0 );

	allowMusicDucking ( 0 );
	music ( "aux1" );


	globalCoop_player_transmittToAll ( "locationprint 0 70 $$M3Stardate$$ 14.0\n" );
	wait ( 2.5 );
	thread globalAccelMove( $entOrigin, '1024 0 0', 15, "rampdown", "" );

	wait ( 4 );

	wait ( 9 );

	thread cinematicEntIntroIGM2End ();
}

void cinematicEntIntroIGM2End ()
{
	globalCoop_player_transmittToAll ( "locationprint 0 70 \"\" 0.5\n" );

	cam_fadeout( 2 , 0, 0, 0, 1, 0 );
	wait ( 2 );

	allowMusicDucking ( 1 );
	//music ( "normal" );

	killthread ( "cinematicEntIntroIGM2" );

	IGMCinematicDone = 1;
}

void cinematicEntIntroIGM4 ()
{
	$entOrigin.followpath ( $entOrbit_2 , "normalangles" );

	cinematic ();
	freezeplayer ();
	letterbox ( .1 );
	wait ( .5 );

	//tighten the cinematic up a little by getting rid of the second camera shot
	spawn ( "Camera" , "targetname" , "entIntroCam" );
	//spawn ( "Camera" , "targetname" , "entIntroCam2" );
	cam.load ( "igm4_EntIntro" );
	//cam.load ( "igm4_EntIntro2" );

	wait ( .1 );
	skipthread ( "cinematicEntIntroIGM4End" );

	$entIntroCam.follow ( $igm4_EntIntro );
	$entIntroCam.cut ();
	$entIntroCam.fov ( 50 );

	$entIntroCam2.follow ( $igm4_EntIntro2 );
	$entIntroCam2.cut ();
	$entIntroCam2.fov ( 50 );

	cuecamera ( $entIntroCam );

	//--- set music
	allowMusicDucking( 0 );
	music ("aux2");

	wait ( 2 );
	cam_fadein( 1, 0, 0, 0, 1, 0 );
	globalCoop_player_transmittToAll ( "locationprint 0 70 $$M5Stardate$$ 10.0\n" );

	wait ( 7.5 );


	//cuecamera ( $entIntroCam2 );
	//cam_fadein( .5, 0, 0, 0, 1, 0 );
	//wait ( 4 );

	thread cinematicEntIntroIGM4End ();
}

void cinematicEntIntroIGM4End ()
{
	globalCoop_player_transmittToAll ( "locationprint 0 70 \"\" 0.5\n" );

	cam_fadeout( 1 , 0, 0, 0, 1, 0 );
	wait ( 1 );
	//--- set music
	allowMusicDucking( 1 );
	music ("normal");

//	noncinematic ();
//	clearletterbox ( .1 );
	killthread ( "cinematicEntIntroIGM4" );
//	wait ( 1 );
//	releaseplayer ();
//	cueplayer ();

//	cam_fadein( .55, 0, 0, 0, 1, 0 );
	IGMCinematicDone = 1;
}

void cinematicEntIntroIGM7 ()
{
	spawn ( "Camera" , "targetname" , "entIntroCam" );
	cam.load ( "igm7_EntIntro" );

	wait ( .1 );
	skipthread ( "cinematicEntIntroIGM7End" );

	$entIntroCam.follow ( $igm7_EntIntro );
	$entIntroCam.cut ();
	cuecamera ( $entIntroCam );

	allowMusicDucking ( 0 );
	music ( "aux3" );

	wait ( 2 );

	$entOrigin.followpath ( $entOrbit , "normalangles" );
	$entIntroCam.watch ( "$entOrigin" );
	wait ( 1 );

	cam_fadein( 1, 0, 0, 0, 1, 0 );

	globalCoop_player_transmittToAll ( "locationprint 0 70 $$IGM7Stardate$$ 10.0\n" );
	waitfor ( $entOrigin );

	$entNacelles.anim ( "blur" );
	globalAccelMove_Relative( $entOrigin, '0 1024 0', .5, "rampup", "" );

	$entSaucer.model ( "models/fx/fx-warpflash-federation.tik" );
	$entBody.remove ();
	$entNacelles.remove ();

	wait ( 1 );
	thread cinematicEntIntroIGM7End ();
}

void cinematicEntIntroIGM7End ()
{
	globalCoop_player_transmittToAll ( "locationprint 0 70 \"\" 0.5\n" );

	cam_fadeout( 1 , 0, 0, 0, 1, 0 );
	wait ( 1 );

	//--- set music
	allowMusicDucking( 1 );
	music ("normal");

	killthread ( "cinematicEntIntroIGM7" );
	wait ( 1 );

	IGMCinematicDone = 1;
}

void cinematicEntInterludeIGM4 ()
{
	cinematic();
	freezeplayer ();
	letterbox ( .1 );

	spawn ( "Camera" , "targetname" , "entIntroCam" );
	spawn ( "Camera" , "targetname" , "entIntroCam2" );
	cam.load ( "igm4_EntInterlude" );
	cam.load ( "igm4_EntInterlude2" );

	wait ( .1 );
	skipthread ( "cinematicEntInterludeIGM4End" );

	$entOrigin.followpath ( $entOrbit_2 , "normalangles" );
	wait ( .25 );
	$entOrigin.endpath ();
	$entOrigin.time ( .1 );
	$entOrigin.MoveForward ( 512 );

	$entIntroCam2.follow ( $igm4_EntInterlude2 );
	$entIntroCam2.fov ( 60 );
	$entIntroCam2.cut ();
	wait ( 1.25 );
	cuecamera ( $entIntroCam2 );

	allowMusicDucking ( 0 );
	music ( "aux4" );

	wait( .25 );

	cam_fadein( 1, 0, 0, 0, 1, 0 );
	wait ( 1 );
	
	
	
	globalCoop_player_transmittToAll ( "locationprint 0 70 $$IGM4MidStardate$$ 10.0\n" );

	wait ( 7 );


	thread cinematicEntInterludeIGM4End ();
}

void cinematicEntInterludeIGM4End ()
{
	globalCoop_player_transmittToAll ( "locationprint 0 70 \"\" 0.5\n" );

	cam_fadeout( 1 , 0, 0, 0, 1, 0 );
	wait ( 1 );

	allowMusicDucking ( 1 );
	music ( "normal" );

	killthread ( "cinematicEntInterludeIGM4" );
	wait ( 1 );

	IGMCinematicDone = 1;
}

void cinematicEntInterludeIGM4b ()
{
	cinematic ();
	freezeplayer ();
	letterbox ( .1 );

	spawn ( "Camera" , "targetname" , "entIntroCam" );
	spawn ( "Camera" , "targetname" , "entIntroCam2" );
	cam.load ( "igm4b_EntInterlude" );
	cam.load ( "igm4b_EntInterlude2" );
	cam.load ( "igm4b_EntInterludeDallasPath" );

	wait ( .1 );

	//$entOrigin.followpath ( $entOrbit_2 , "normalangles" );
	//wait ( .25 );
	//$entOrigin.endpath ();
	//$entOrigin.time ( .1 );
	//$entOrigin.MoveForward ( 512 );

	$entIntroCam.follow ( $igm4b_EntInterlude );
	$entIntroCam.fov ( 60 );
	$entIntroCam.cut ();
	wait ( .75 );

	$dallasOrigin.followpath ( $igm4b_EntInterludeDallasPath );
	cuecamera ( $entIntroCam );

	allowMusicDucking ( 0 );
	music ( "aux3" );

	wait( .25 );
	cam_fadein( 1, 0, 0, 0, 1, 0 );
	wait ( 1 );
	globalCoop_player_transmittToAll ( "locationprint 0 70 $$IGM4Stardate$$ 10.0\n" );

	$picard.playdialog( "localization/sound/dialog/ent_m4/picard_sb12.mp3", 1, 10000, 1); //Stardate 6571.4 We have finished repairs to the warp drive on the U.S.S. Dallas. She's ready to head back to Starbase 12 for a refit.

	skipthread ( "cinematicEntInterludeIGM4bEnd" );

	waitfor ( $dallasOrigin );

	globalAccelMove_Relative( $dallasOrigin, '0 1024 0', .5, "rampup", "" );

	$dallasSaucer.model ( "models/fx/fx-warpflash-federation.tik" );
	$dallasSaucer.scale ( .1 );
	$dallasBody.remove ();

	wait ( 1.5 );
	thread cinematicEntInterludeIGM4bEnd ();
}

void cinematicEntInterludeIGM4bEnd ()
{
	globalCoop_player_transmittToAll ( "locationprint 0 70 \"\" 0.5\n" );

	$picard.stopDialog();
	
	
	//hzm coop mod chrissstrahl - make all dialog sounds stop for all the players
	entity ePlayer;
	float fPlayerIdInUse;
	for(fPlayerIdInUse=0;fPlayerIdInUse<coop_integer_maxPlayers;fPlayerIdInUse++){
		ePlayer = getentity("player"+fPlayerIdInUse);
		if(doesEntityExist(ePlayer)){
			ePlayer.killalldialog();
		}
	}
	
	
	cam_fadeout( 1 , 0, 0, 0, 1, 0 );
	wait ( 1 );

	allowMusicDucking ( 1 );
	music ( "normal" );


	killthread ( "cinematicEntInterludeIGM4b" );
	wait ( 1 );

	IGMCinematicDone = 1;
}


