//-----------------------------------------------------------------
//
//  EF2 Level Script File
//
//  Level:	      ent-deck11 - MISSION 3
//  Script By:    K.Thompson
//  Created on:   08/12/2002
//	Last Edited:  08/12/2002 - kt
//-----------------------------------------------------------------

void setupMission3Deck11();
void justLoadedDeckThreadIGM3();
void playerReachedSecretIGM3();

void queTelsiaForIGM3Cinematic();

void cinematicTelsiaGreetsMunro();
void entDeck8IGM3Option1();
void entDeck8IGM3Option2();

void useLibraryTerminalIGM3();
void useLibraryTerminalIGM3Page0();
void useLibraryTerminalIGM3Page1();
void useLibraryTerminalIGM3Page2();
void useLibraryTerminalIGM3Page3();
void useLibraryTerminalIGM3Page4();
void useLibraryTerminalIGM3Page5();
void useLibraryTerminalIGM3Page6();

void telsiaTalkingDialogPage1IGM3();
void telsiaTalkingDialogPage2IGM3();
void telsiaTalkingDialogPage3IGM3();

void telsia_workIGM3();
void telsiaDialog3_1();

void picardCommsMunroIGM3();

void ambientActorsIGM3();

void alisonrusselTalkIGM3();
void hideAlisonAndRusselIGM3();
void sendAlisonAndRusselOnPatrolIGM3();

void miyamoto_workIGM3();
void miyamotoDialog3_1();
void miyamotoDialog3_2();
void miyamotoDialog3_3();

float telsiaFollowsMunroIGM3 = 0;
float numberOfTimesTalkedWithTelsiaIGM3 = 0;

//hzm coop mod chrissstrahl, use global player entity reference for libarary menu
entity ePlayerResarching;
entity ePreviousePannel;
string sCurrentMenu;

//hzm coop mod chrissstrahl - failsafe for branch dialog
void entDeck11IGM3BranchingDialog_failsafe();


//===================================================================================================================
// Setup 
//===================================================================================================================

//---------------------
// setupMission3Deck11	
// does all the setup stuff for the mission
//---------------------
void setupMission3Deck11()
{
	spawn( "Camera", "targetname", "cam1" );
	spawn( "Camera", "targetname", "cam2" );
	spawn( "Camera", "targetname", "cam3" );
	spawn( "Camera", "targetname", "cam4" );
	spawn( "Camera", "targetname", "cam5" );
	spawn( "Camera", "targetname", "cam6" );
	spawn( "Camera", "targetname", "cam7" );
			
	cam.load( "cam_igm3_deck11_1" );
	cam.load( "cam_igm3_deck11_2" );
	cam.load( "cam_igm3_deck11_3" );
	cam.load( "cam_igm3_deck11_4" );
	cam.load( "cam_igm3_deck11_5" );
	cam.load( "cam_igm3_deck11_6" );
	cam.load( "cam_igm3_deck11_7" );
	
	globalCommon_SpawnActor( "char/hazardteam_sf-munro.tik", "scriptmunro", '-10116 -596 0', 295 );
	
	globalCommon_SpawnActor( "char/hazardteam_sf-telsia.tik", "telsia", '-10144 -1344 4', 90 );
	
	$scriptmunro.hide();
	$scriptmunro.notsolid();
	
	globalArchetype_Setup ( $triggerLibraryUse1Archetype, "LibraryTerminal" );
	$triggerLibraryUse1Archetype.missionobjective( 1 );
	
	ambientActorsIGM3();
	
	$munroApproachingLibrary.thread( "queTelsiaForIGM3Cinematic" ); 
	
	//Setup Secret
	//Unlock Secret Door.
	$secretDoor.unlock();
	$secretTrigger.thread( "playerReachedSecretIGM3" );
	spawn( "trigger_secret", "targetname", "secretSpawner_trigger" );
	trigger( "$secretSpawner" );
	
	//hzm coop mod Chrissstrahl, set the objective
	globalCoop_objectives_set("$$IGM3TalkWithPicard$$","complete",1,0);
	globalCoop_objectives_set("$$IGM3GoToLibaray$$","incomplete",2,1);
}

//---------------------
// justLoadedDeckThreadIGM1
// Triggered and check to see if A cinematic should happen.
//------------------------
void justLoadedDeckThreadIGM3()
{
}

//---------------------
// used to place secret pieces in level 
//------------------------
void playerReachedSecretIGM3()
{
	$secretTrigger.thread( "" );
	trigger( "$secretSpawner_trigger" );
}

//---------------------
// queTelsiaForIGM3Cinematic	
// Make Telsia Exit Libaray To talk with Munro.
//---------------------
void queTelsiaForIGM3Cinematic()
{
	//hzm coop mod chrissstrahl, get player that activated this sequence, for later reference
	entity eTrigger;
	eTrigger			= getCurrentEntity();
	entityPlayerGlobal	= eTrigger.getLastActivatingEntity();
	
	$munroApproachingLibrary.thread( "" ); 

	$telsia.walkto( "telsiaCinematicNode1" );
	wait( 1 );
	thread cinematicTelsiaGreetsMunro();
}

//---------------------
// cinematicTelsiaGreetsMunro
// setup and begin cinematicTelsiaGreetsMunro
//---------------------
void cinematicTelsiaGreetsMunro()
{
	//--- Fade Out
	cam_fadeout( .5, 0, 0, 0, 1, 0 );
	wait ( .75 );

	freezeplayer();
	cinematic();
	
	// --- Setup
	
	$scriptmunro.show();
	//$scriptmunro.solid();
	
	hideAlisonAndRusselIGM3();
	
	// --- Que Cams
	$cam1.follow ( $cam_igm3_deck11_1 );
	$cam2.follow ( $cam_igm3_deck11_2 );
	$cam3.follow ( $cam_igm3_deck11_3 );
	$cam4.follow ( $cam_igm3_deck11_4 );
	$cam5.follow ( $cam_igm3_deck11_5 );
	$cam6.follow ( $cam_igm3_deck11_6 );
	$cam7.follow ( $cam_igm3_deck11_7 );
	
	$cam1.fov( 60 );
	$cam2.fov( 10 );
	$cam3.fov( 10 );
	$cam4.fov( 30 );
	$cam5.fov( 30 );
	$cam6.fov( 60 );
	$cam7.fov( 60 );

	$cam1.cut();
	$cam2.cut();
	$cam3.cut();
	$cam4.cut();
	$cam5.cut();
	$cam6.cut();
	$cam7.cut();
	
	cuecamera ( $cam1 );
	letterbox( .05 );
	
	// --- Skip Thread
	//No Skip thread allowed
	//skipthread( "cinematicTelsiaGreetsMunroSkipthread");
	
	$telsia.walkto( "telsiaCinematicNode1" );
	$scriptmunro.walkto( "telsiaCinematicNode2" );
	
	wait( .05 );
	
	//--- Start & Fade in
	cam_fadein( .5, 0, 0, 0, 1, 0 );
	wait( .6 );
	
	$telsia.walkto( "telsiaCinematicNode1" );
	$scriptmunro.walkto( "telsiaCinematicNode2" );
	waitfor( $scriptmunro );
	
	$telsia.walkto( "telsiaCinematicNode1" );
	waitfor( $telsia );
	
	//this is a test, should be removed before release
	//if(getCvar("username") != "Chrissstrahl"){
		$telsia.playdialog("localization/sound/dialog/ent_m3/telsia_himunro2.mp3", 1, 10000, 0); //Hi, Munro.
		waitForDialog( $telsia );
		
		cuecamera ( $cam3 );
		$scriptmunro.playdialog("localization/sound/dialog/ent_m3/munro_up.mp3", 1, 10000, 0); //Telsia. I'm glad to see you up and around.
		waitForDialog( $scriptmunro );
		
		cuecamera ( $cam2 );
		$telsia.playdialog("localization/sound/dialog/ent_m3/telsia_fine.mp3", 1, 10000, 0); //I'm fine. There's no lasting damage from my time in the pattern buffer.
		waitForDialog( $telsia );
		
		cuecamera ( $cam3 );
		$scriptmunro.playdialog("localization/sound/dialog/ent_m3/munro_lucky.mp3", 1, 10000, 0); //You're lucky. That was quite a stunt.
		waitForDialog( $scriptmunro );

		cuecamera ( $cam2 );
		$telsia.playdialog("localization/sound/dialog/ent_m3/telsia_ready.mp3", 1, 10000, 0); //I’m ready to go back on duty.
		waitForDialog( $telsia );
		
		cuecamera ( $cam3 );
		$scriptmunro.playdialog("localization/sound/dialog/ent_m3/munro_iwant.mp3", 1, 10000, 0); //Good. Because I want you and Gonzalez on the Hazard Team.
		waitForDialog( $scriptmunro );
		
		cuecamera ( $cam2 );
		$telsia.playdialog("localization/sound/dialog/ent_m3/telsia_gladgonz.mp3", 1, 10000, 0); //I'm glad to see Gonzalez on the team. He's an experienced combat tactician.
		waitForDialog( $telsia );
		
		cuecamera ( $cam3 );
		$scriptmunro.playdialog("localization/sound/dialog/ent_m3/munro_almost.mp3", 1, 10000, 0); //Almost as experienced as you. I need your expertise to help me figure out these aliens.
		waitForDialog( $scriptmunro );
		
		cuecamera ( $cam4 );
		$telsia.playdialog("localization/sound/dialog/ent_m3/telsia_thatall.mp3", 1, 10000, 0); //Is that all you need?
		waitForDialog( $telsia );
		
		cuecamera ( $cam5 );
		$telsia.branchdialog( "entDeck11IGM3BranchingDialog" );
		pause();
	//}else{
		//wait(2);
		//hzm coop mod chrissstrahl, at this time we want telsia to go with us
		//thread entDeck8IGM3Option1();	
	//}
}

//////////////////////////////////////
//  entDeck8IGM3Option1
//  Optional Text and Dialog 1
//////////////////////////////////////
void entDeck8IGM3Option1()
{
	//hzm coop mod chrissstrahl, allow this thread to be started only once
	if(	$world.getFloatVar("entDeck8IGM3Option1") != 0){return;}
	else{$world.setFloatVar("entDeck8IGM3Option1",1);}

	$triggerLibraryUse1.thread( "useLibraryTerminalIGM3" );
	//this makes sure you are forced to stand near telsia if she is there
	$triggerLibraryUse2.thread( "" );
	$triggerLibraryUse3.thread( "" );
	$triggerLibraryUse4.thread( "" );
	
	$scriptmunro.playdialog("localization/sound/dialog/ent_m3/munro_helpme.mp3", 1, 10000, 0 ); //I wouldn't mind having you help me do some research on the Attrexians.
	waitForDialog( $scriptmunro );
	
	//Increase Telsia Float Var
	globalCommon_LoveChoice_Telsia(); 
	
	cuecamera ( $cam7 );
	
	wait( .2 );
	$telsia.turnToAngle( 0 );
	wait( .5 );
	$telsia.anim( "walk" );
	wait( 1.5 );
	
	telsiaFollowsMunroIGM3 = 1;
	
	//--- fade out and set the level up
	cam_fadeout( .5, 0, 0, 0, 1, 0 );
	wait( .6 );
	
	//hzm coop mod chrissstrahl, grab player that was triggering this sequence, and move him, like in singleplayer
	if( doesEntityExist(entityPlayerGlobal) && entityPlayerGlobal.getHealth() > 0){
		entityPlayerGlobal.origin( $scriptmunro.getorigin() );
	}
	
	$scriptmunro.hide();
	$scriptmunro.notsolid();
	
	$telsia.origin( '-9580 -978  0' );
	globalCommon_AiActorUseWeapon( $telsia, "tricorder" );
	$telsia.angle( 0 );
	$telsia.anim( "tricorder_use_loop" );
	$telsia.resethead();
	
	sendAlisonAndRusselOnPatrolIGM3();

	cueplayer();
	noncinematic();
	releaseplayer();
	clearletterbox( .1 );
	
	cam_fadein( .5, 0, 0, 0, 1, 0 ) ;
	
//	wait( .6 );	
	
//	$telsia.walkto( "libraryNode0" );
//	waitfor( $telsia );
}

//////////////////////////////////////
//  entDeck8IGM3Option2
//  Optional Text and Dialog 2
//////////////////////////////////////
void entDeck8IGM3Option2()
{
	//hzm coop mod chrissstrahl, allow this thread to be started only once
	if(	$world.getFloatVar("entDeck8IGM3Option1") != 0){return;}
	else{$world.setFloatVar("entDeck8IGM3Option1",1);}
	
	$triggerLibraryUse1.thread( "useLibraryTerminalIGM3" );
	$triggerLibraryUse2.thread( "useLibraryTerminalIGM3" );
	$triggerLibraryUse3.thread( "useLibraryTerminalIGM3" );
	$triggerLibraryUse4.thread( "useLibraryTerminalIGM3" );
	
	// triggerLibraryUse1Archetype isn't called here to prevent the objective from being removed to early
	globalArchetype_Setup ( $triggerLibraryUse2Archetype, "LibraryTerminal" );
	globalArchetype_Setup ( $triggerLibraryUse3Archetype, "LibraryTerminal" );
	globalArchetype_Setup ( $triggerLibraryUse4Archetype, "LibraryTerminal" );

	$scriptmunro.playdialog("localization/sound/dialog/ent_m3/munro_thatsit.mp3", 1, 10000, 0 ); //Yeah, that's about it.
	waitForDialog( $scriptmunro );

	cuecamera ( $cam6 );
	
	wait( .2 );
	$telsia.turnToAngle( 270 );
	wait( .5 );
	$telsia.anim( "walk" );
	wait(.5 );

	//--- fade out and set the level up
	cam_fadeout( 2, 0, 0, 0, 1, 0 );
	wait( 4.1 );
	
	//hzm coop mod chrissstrahl, grab player that was triggering this sequence, and move him, like in singleplayer
	if( doesEntityExist(entityPlayerGlobal) && entityPlayerGlobal.getHealth() > 0){
		entityPlayerGlobal.origin( $scriptmunro.getorigin() );
	}
	
	$scriptmunro.hide();
	$scriptmunro.notsolid();
	
	$telsia.remove();
	telsiaFollowsMunroIGM3 = 0;
	
	sendAlisonAndRusselOnPatrolIGM3();
		
	cueplayer();
	noncinematic();
	releaseplayer();
	clearletterbox( .1 );
	
	cam_fadein( .5, 0, 0, 0, 1, 0 ) ;
	wait( .6 );
}

void coop_checkIfPlayerIsValid(entity ePlayer, entity eTrigger)
{

	if(doesEntityExist(ePlayer) && doesEntityExist(eTrigger)){
		ePlayer.setFloatVar("librarayTerminalUseStop",0);
		float fCurrentPlayerValid = 1;
		vector v1,v2;
		while(fCurrentPlayerValid)
		{
			wait(0.1);
			
			if(doesEntityExist(ePlayer)){
				
				v1 = ePlayer.getOrigin();
				v2 = eTrigger.getOrigin();
			
				if(ePlayer.getHealth() < 1){
					ePlayer.removehud(sCurrentMenu);
					fCurrentPlayerValid = 0;
				}else if( vectorLength( v1 - v2 ) > 200 ){
					ePlayer.removehud(sCurrentMenu);
					fCurrentPlayerValid = 0;
				}else if(globalCoop_check_playerSpectator(ePlayer)){
					ePlayer.removehud(sCurrentMenu);
					fCurrentPlayerValid = 0;
				}else if(ePlayer.getFloatVar("librarayTerminalUseStop") == 1){
					ePlayer.removehud(sCurrentMenu);
					fCurrentPlayerValid = 0;
				}
				if(fCurrentPlayerValid == 0){
					globalCoop_player_transmitter(ePlayer,"ui_hidemouse\n");
				}
			}else{
				fCurrentPlayerValid = 0;
			}
		}

		eTrigger.thread( "useLibraryTerminalIGM3" );
		ePlayerResarching = $world;
	}
}

//////////////////////////////////////
//  useLibraryTerminalIGM3
//  Function wait untill a libaray terminal is used then bring up the UI
// Also should change objective
//////////////////////////////////////
void useLibraryTerminalIGM3()
{
//hzm coop mod chrissstrahl, store player that accessed this pannel, for later reference
	entity eTrigger,ePlayer;
	eTrigger	= getCurrentEntity();
	if(doesEntityExist(eTrigger)){
		ePlayer				= eTrigger.getLastActivatingEntity();
		if(doesEntityExist(ePlayer)){
			ePlayerResarching	= ePlayer;
		}
		
	}else{
		return;
	}
	
	thread coop_checkIfPlayerIsValid(ePlayer,eTrigger);

	//hzm coop mod chrissstrahl - print some info into the game console
	//globalCoop_main_print("useLibraryTerminalIGM3");
	
	$triggerLibraryUse1.thread( "" );
	$triggerLibraryUse2.thread( "" );
	$triggerLibraryUse3.thread( "" );
	$triggerLibraryUse4.thread( "" );
	
	//Objectives Stuff
	setfloatvar( "game.goToLibaryIGM3", 2 );
	justLoadedDeckThreadIGM3();
	
	useLibraryTerminalIGM3Page1();

	if( getfloatvar ("game.checkEntMission3RoomsVisited" ) == 1 )
	{
		// Set this var so the IMG Holo deck mission Will play
		setfloatvar ("game.checkEntMission3RoomsVisited", 2 );
	}
}

//////////////////////////////////////
//  useLibraryTerminalIGM3
//  Function to remove any libaray terminal UI up and wait till it is used agian
//////////////////////////////////////
void useLibraryTerminalIGM3Page0()
{	
	//hzm coop mod chrissstrahl, select each time a different pannel
	//this should make it easier for every player to get a chance
	$triggerLibraryUse1.thread( "" );
	$triggerLibraryUse2.thread( "" );
	$triggerLibraryUse3.thread( "" );
	$triggerLibraryUse4.thread( "" );
	
	float fPannel;
	entity ePannelTrigger;
	
	fPannel = getFloatVar("level.currentLibrarayPannel");
	if( fPannel == 0 || fPannel >= 4){
		fPannel = 2;
	}else{
		fPannel++;
	}
	setFloatVar("level.currentLibrarayPannel",fPannel);
	
	if(fPannel == 2){$triggerLibraryUse1Archetype.origin( '-9554 -777 56' );}
	else if(fPannel == 3){$triggerLibraryUse1Archetype.origin( '-9554 -608 56' );}
	else if(fPannel == 4){$triggerLibraryUse1Archetype.origin( '-9776 -410 56' );}
	
	ePannelTrigger	= getEntity("triggerLibraryUse"+fPannel);
	ePannelTrigger.thread("useLibraryTerminalIGM3Page1");

/*
	//this makes sure you are forced to stand near telsia if she is there
	if( telsiaFollowsMunroIGM3 == 0 )
	{
		//hzm coop mod chrissstrahl, we only want one trigger to work or it will breake the script
		//$triggerLibraryUse1.thread( "useLibraryTerminalIGM3Page1" );
		//$triggerLibraryUse3.thread( "useLibraryTerminalIGM3Page1" );
		//$triggerLibraryUse4.thread( "useLibraryTerminalIGM3Page1" );
		$triggerLibraryUse2.thread( "useLibraryTerminalIGM3Page1" );
	}
	else
	{
		//hzm coop mod chrissstrahl, we only want one trigger to work or it will breake the script
		$triggerLibraryUse2.thread( "useLibraryTerminalIGM3Page1" );
		//$triggerLibraryUse1.thread( "" );
		//$triggerLibraryUse3.thread( "" );
		//$triggerLibraryUse4.thread( "" );*
	}
*/
	//removes hud when done button is pressed.
	ePlayerResarching.removehud( "ent-console_page6-IGM3" );
	ePlayerResarching.playsound( "sound/misc/menu/reject.wav", 10 ,0.75 ,100 );
	ePlayerResarching.setFloatVar("librarayTerminalUseStop",1);
	sCurrentMenu = "";

	//This makes sure the player can traverse the menu system untill he exits
	//Problem with out: Play coun't access page 1 after first time
	if( numberOfTimesTalkedWithTelsiaIGM3 == 3 )
	{
		if( telsiaFollowsMunroIGM3 == 1 )
		{
			numberOfTimesTalkedWithTelsiaIGM3 ++;
			thread telsia_workIGM3();
		}
	}

	// stop any dialog currently happening
	killthread( "telsiaTalkingDialogPage1IGM3" );
	killthread( "telsiaTalkingDialogPage2IGM3" );
	killthread( "telsiaTalkingDialogPage3IGM3" );
	killAllTalking();
		
		
	if(getFloatVar("level.ResarchCompleteAftermath") == 0){
		setFloatVar("level.ResarchCompleteAftermath",1);
		$triggerLibraryUse1Archetype.missionobjective( 0 );
		
		//picard can now call munro
		$triggerLibrary.thread( "picardCommsMunroIGM3" );
		
		//Turn Exit Triggers On
		$triggerExit.triggerable();
	}
}

void useLibraryTerminalIGM3Page1()
{
	if(!doesEntityExist(ePlayerResarching) || ePlayerResarching.getHealth() < 1){
		return;
	}
	sCurrentMenu = "ent-console_page1-IGM3";
	
	//if( telsiaFollowsMunroIGM3 == 1 )
	//	if( numberOfTimesTalkedWithTelsiaIGM3 == 4 )
	//		return;
	
	//add Hud Stuff here
	ePlayerResarching.removehud( "ent-console_page1-IGM3" );
	ePlayerResarching.removehud( "ent-console_page2-IGM3" );
	ePlayerResarching.addhud( "ent-console_page1-IGM3" );
	
	//added to make sure the menu does not get buggy
	globalCoop_player_transmittInstantly(ePlayerResarching,"ui_showmouse");
	
	ePlayerResarching.playsound( "sound/misc/menu/select.wav", 10 ,0.75 ,100 );
		
	// This check to see if telsia is there and or needs
	// to speak.
	if( numberOfTimesTalkedWithTelsiaIGM3 == 0 )
	{
		if( telsiaFollowsMunroIGM3 == 1 )
		{
			numberOfTimesTalkedWithTelsiaIGM3 ++;
			thread telsiaTalkingDialogPage1IGM3();
		}
	}
}

void useLibraryTerminalIGM3Page2()
{
	if(!doesEntityExist(ePlayerResarching) || ePlayerResarching.getHealth() < 1){
		return;
	}
	//add Hud Stuff here
	sCurrentMenu = "ent-console_page2-IGM3";
	ePlayerResarching.addhud( "ent-console_page2-IGM3" );
	ePlayerResarching.removehud( "ent-console_page1-IGM3" );
	ePlayerResarching.removehud( "ent-console_page3-IGM3" );
	
	ePlayerResarching.playsound( "sound/misc/menu/select.wav", 10 ,0.75 ,100 );
}

void useLibraryTerminalIGM3Page3()
{
	if(!doesEntityExist(ePlayerResarching) || ePlayerResarching.getHealth() < 1){
		return;
	}
	//add Hud Stuff here
	sCurrentMenu = "ent-console_page3-IGM3";
	ePlayerResarching.addhud( "ent-console_page3-IGM3" );
	ePlayerResarching.removehud( "ent-console_page2-IGM3" );
	ePlayerResarching.removehud( "ent-console_page4-IGM3" );
	
	ePlayerResarching.playsound( "sound/misc/menu/select.wav", 10 ,0.75 ,10000 );

	// This check to see if telsia is there and or needs
	// to speak.
	if( numberOfTimesTalkedWithTelsiaIGM3 == 1 )
	{
		if( telsiaFollowsMunroIGM3 == 1 )
		{
			numberOfTimesTalkedWithTelsiaIGM3 ++;
			thread telsiaTalkingDialogPage2IGM3();
		}
	}
}

void useLibraryTerminalIGM3Page4()
{
	if(!doesEntityExist(ePlayerResarching) || ePlayerResarching.getHealth() < 1){
		return;
	}

	//add Hud Stuff here
	sCurrentMenu = "ent-console_page4-IGM3";
	ePlayerResarching.addhud( "ent-console_page4-IGM3" );
	ePlayerResarching.removehud( "ent-console_page3-IGM3" );
	ePlayerResarching.removehud( "ent-console_page5-IGM3" );
	
	ePlayerResarching.playsound( "sound/misc/menu/select.wav", 10 ,0.75 ,10000 );
}

void useLibraryTerminalIGM3Page5()
{
	//add Hud Stuff here
	if(!doesEntityExist(ePlayerResarching) || ePlayerResarching.getHealth() < 1){
		return;
	}
	sCurrentMenu = "ent-console_page5-IGM3";
	ePlayerResarching.addhud( "ent-console_page5-IGM3" );
	ePlayerResarching.removehud( "ent-console_page4-IGM3" );
	ePlayerResarching.removehud( "ent-console_page6-IGM3" );
	
	ePlayerResarching.playsound( "sound/misc/menu/select.wav", 10 ,0.75 ,10000 );

	// This check to see if telsia is there and or needs
	// to speak.
	if( numberOfTimesTalkedWithTelsiaIGM3 == 2 )
	{
		if( telsiaFollowsMunroIGM3 == 1 )
		{
			numberOfTimesTalkedWithTelsiaIGM3 ++;
			thread telsiaTalkingDialogPage3IGM3();
		}
	}
}

void useLibraryTerminalIGM3Page6()
{
	if(!doesEntityExist(ePlayerResarching) || ePlayerResarching.getHealth() < 1){
		return;
	}
	//add Hud Stuff here
	sCurrentMenu = "ent-console_page6-IGM3";
	ePlayerResarching.addhud( "ent-console_page6-IGM3" );
	ePlayerResarching.removehud( "ent-console_page5-IGM3" );
	
	ePlayerResarching.playsound( "sound/misc/menu/select.wav", 10 ,0.75 ,10000 );
}


void telsiaTalkingDialogPage1IGM3()
{
	$telsia.playdialog("localization/sound/dialog/ent_m3/telsia_suspenct.mp3", 1, 10000, 1); //Hmm... no usual suspect here. The Attrexians seem to have made very few enemies. 
	waitForDialog( $telsia );
	$scriptmunro.playdialog("localization/sound/dialog/ent_m3/munro_pretty.mp3", 1, 10000, 1); //Pretty good for an expanding mini-empire with over twenty colony planets. 
	waitForDialog( $scriptmunro );
	$telsia.playdialog("localization/sound/dialog/ent_m3/telsia_neutral.mp3", 1, 10000, 1); //Especially a mini-empire so close to the Neutral Zone.
	waitForDialog( $telsia );
	$scriptmunro.playdialog("localization/sound/dialog/ent_m3/munro_expand.mp3", 1, 10000, 1); //They were lucky enough to expand into systems not claimed by other races. 
	waitForDialog( $scriptmunro );
	$telsia.playdialog("localization/sound/dialog/ent_m3/telsia_romulans.mp3", 1, 10000, 1); //And smart enough to stay out of the way of the Romulans. 
	waitForDialog( $telsia );
}


void telsiaTalkingDialogPage2IGM3()
{
	killthread( "telsiaTalkingDialogPage1IGM3" );
	killAllTalking();
	
	$scriptmunro.playdialog("localization/sound/dialog/ent_m3/munro_races.mp3", 1, 10000, 1); //There's some unrest on Attrexian planets populated by other races.
	waitForDialog( $scriptmunro );
	$telsia.playdialog("localization/sound/dialog/ent_m3/telsia_empires.mp3", 1, 10000, 1); //Same with many stellar empires.
	waitForDialog( $telsia );
	$scriptmunro.playdialog("localization/sound/dialog/ent_m3/munro_smugg.mp3", 1, 10000, 1); //Ongoing problem with smugglers. 
	waitForDialog( $scriptmunro );
	$telsia.playdialog("localization/sound/dialog/ent_m3/telsia_same.mp3", 1, 10000, 1); //Same with the Federation. 
	waitForDialog( $telsia );
	$scriptmunro.playdialog("localization/sound/dialog/ent_m3/munro_angry.mp3", 1, 10000, 1); //But no angry species of semi-intelligent, exoskeletal aliens.
	waitForDialog( $scriptmunro );

}

void telsiaTalkingDialogPage3IGM3()
{
	killthread( "telsiaTalkingDialogPage2IGM3" );
	killAllTalking();
	
	$telsia.playdialog("localization/sound/dialog/ent_m3/telsia_candidate.mp3", 1, 10000, 1); //The Attrexians are a Federation candidate. If they join, Starfleet will get some prime bases near the Neutral Zone. Does that give the Romulans a motivation to attack them?
	waitForDialog( $telsia );
	$scriptmunro.playdialog("localization/sound/dialog/ent_m3/munro_private.mp3", 1, 10000, 1); //With what? Their private army of semi-intelligent, exoskeletal aliens? 
	waitForDialog( $scriptmunro );
	$telsia.playdialog("localization/sound/dialog/ent_m3/telsia_invasion.mp3", 1, 10000, 1); //Then we may be looking at the first wave of an extra-dimensional invasion.
	waitForDialog( $telsia );
}

///////////////////////////
// telsia_workIGM3
// Wait for player to talk to actor
///////////////////////////
void telsia_workIGM3()
{
	setNextDialog( $telsia, 1 );
	setDialogFunction( $telsia, "telsiaDialog" );
	while( 1 )
	{
		workUntilUsed( $telsia, "", "", 0 );
		talkUntilDone( $telsia );
	}
}

///////////////////////////
// telsiaDialog3_1
// Player and actor talk
///////////////////////////
void telsiaDialog3_1()
{	
	$telsia.anim( "tricorder_scan-look" );
	$telsia.headwatch( $player, 10 );
	$telsia.turntowardsplayer();

	$telsia.playdialog("localization/sound/dialog/ent_m4/telsia_timetog.mp3", 1, 128, 0); //Are you going to finish that research?
	waitForDialog( $telsia );

	setNextDialog( $telsia, 1 );
	$telsia.resethead();
	$telsia.angle( 0 );
	$telsia.anim( "tricorder_use_loop" );
	setDialogFinished( $telsia );
}

//////////////////////////
// Summons Munro to deck 8
//////////////////////////
void picardCommsMunroIGM3()
{
	$triggerLibrary.thread( "" );
		
	globalCommon_SpawnActor( "char/starfleet_picard.tik", "picard", '-10200 -1308 0', 270 );
	$picard.hide();
	$picard.notsolid();
	
	wait( .5 );
	
	$picard.playdialog("localization/sound/dialog/ent_m3/picard_reportbrief.mp3", 1, 10000, 1); //Lieutenant Munro.  Report to the Hazard Team briefing room.
	waitForDialog( $picard );
	$scriptmunro.playdialog("localization/sound/dialog/ent_m3/munro_onway2.mp3", 1, 10000, 1); //On my way.
	waitForDialog( $scriptmunro );
	
	//[b611] chrissstrahl - make sure no player can get stuck in menu
	entity ePlayer;
	float fPlayerIdInUse=0;
	while(fPlayerIdInUse<coop_integer_maxPlayers){
		ePlayer = getentity("player"+ fPlayerIdInUse);
		if(doesEntityExist(ePlayer)){
			ePlayer.removehud( "ent-console_page1-IGM3" );
			ePlayer.removehud( "ent-console_page2-IGM3" );
			ePlayer.removehud( "ent-console_page3-IGM3" );
			ePlayer.removehud( "ent-console_page4-IGM3" );
			ePlayer.removehud( "ent-console_page5-IGM3" );
			ePlayer.removehud( "ent-console_page6-IGM3" );
		}
		fPlayerIdInUse++;
	}
	
	wait( .5 );
	
	//Objectives
	setfloatvar( "game.IGM3MeetInBriefingRoom", 1 );
	justLoadedDeckThreadIGM3();
	
	//hzm coop mod chrissstrahl, allow player now to use turbolift to deck 7a
	setfloatvar(  "level.deckToChangeToIGM", 71 );
	
	//hzm coop mod Chrissstrahl, set the objective
	globalCoop_objectives_set("$$IGM3GoToLibaray$$","complete",2,0);
	globalCoop_objectives_set("$$IGM3MeetInBriefingRoom$$","incomplete",3,1);
}

//===================================================================================================================
// Ambient Actors
//===================================================================================================================
//---------------------
// ambientActorsIGM3	
// Set up ambient Actors
//---------------------
void ambientActorsIGM3()
{
	globalCommon_SpawnActor( "char/starfleet_crew-female2.tik", "miyamoto", '-9804.00 -836.00  4.00', 0 );
		
	thread miyamoto_workIGM3();
	
	thread randomlyAnimateActor( $miyamoto, "ent-library-seat" );

	globalCommon_SpawnActor( "char/starfleet_crew-female5.tik", "alison", '-9640 596 0', 290 );
	$alison.pushable( 0 );
	globalCommon_SpawnActor( "char/starfleet_crew-male5.tik", "russel", '-9624 548 0', 110 );
	$russel.pushable( 0 );

	$triggerHallConversation1.thread( "alisonrusselTalkIGM3" );

	$alison.headwatch( $russel, 5 );
	$russel.headwatch( $alison, 5 );

	thread randomlyAnimateActor( $alison, "conv" );
	thread randomlyAnimateActor( $russel, "conv" );
}

////////////////////////////
// Actors Converse
/////////////////////////////
void alisonrusselTalkIGM3()
{
	$triggerHallConversation1.thread( "" );

	$alison.setFloatVar( "pauseRandomAnimate", 1 );
	$russel.setFloatVar( "pauseRandomAnimate", 1 );
	wait( .25 );
	$alison.anim( "idle" );
	$russel.anim( "idle" );
	
	$alison.playdialog("localization/sound/dialog/ent_m3/alison_justtell.mp3", 1, 128, 0); //Would you like to join me in the holodeck?  I’m starting a holoprogram to view the flora of Andoria.
	waitForDialog( $alison );
	$russel.playdialog("localization/sound/dialog/ent_m3/crewm_forlastt.mp3", 1, 128, 0); //Sounds interesting.
	waitForDialog( $russel );
	$alison.playdialog("localization/sound/dialog/ent_m3/alison_nerv.mp3", 1, 128, 0); //You might want to bring a weapon.
	waitForDialog( $alison );
	$russel.playdialog("localization/sound/dialog/ent_m3/crewm_donttime.mp3", 1, 128, 0); //What do we need weapons for?
	waitForDialog( $russel );
	$alison.playdialog("localization/sound/dialog/ent_m3/alison_certf.mp3", 1, 128, 0); //The flora are not necessarily friendly.  In fact they’re carnivorous.
	waitForDialog( $alison );
	$russel.playdialog("localization/sound/dialog/ent_m3/crewm_subtle.mp3", 1, 128, 0); //What about the safety protocols?
	waitForDialog( $russel );
	$alison.playdialog("localization/sound/dialog/ent_m3/alison_sweet.mp3", 1, 128, 0); //How do you study plants in their environment if you eliminate their ability to interact with their surroundings?
	waitForDialog( $alison );
	$russel.playdialog("localization/sound/dialog/ent_m3/crewm_callsec.mp3", 1, 128, 0); //I don’t really think I’m up for being eaten by a plant today.
	waitForDialog( $russel );
	$alison.playdialog("localization/sound/dialog/ent_m3/alison_special.mp3", 1, 128, 0); //Come on.  It’ll be fun.
	waitForDialog( $alison );
	$russel.playdialog("localization/sound/dialog/ent_m3/crewm_itwasnt.mp3", 1, 128, 0); //I just remembered I have an appointment with the ship’s counselor.
	waitForDialog( $russel );

	$alison.setFloatVar( "pauseRandomAnimate", 0 );
	$russel.setFloatVar( "pauseRandomAnimate", 0 );
	
	$alison.resethead();
	$russel.resethead();
	
	$alison.setnodeid( 50 );
	$alison.alias( "alisonIGMA1", "localization/sound/dialog/ent_m1/alison_kindaBusy.mp3" ); //Please don’t disturb me.
	$alison.dialog( "alisonIGMA" );
	
	$russel.setnodeid( 51 );
	$russel.alias( "russelIGMA1", "localization/sound/dialog/ent_m2/stratton_busy1.mp3" ); //Sorry.  No time for chit-chat.
	$russel.dialog( "russelIGMA" );
	
	//This is here to make them patrol when necessary
	$alison.ai_on();
	$russel.ai_on();
}

// Hide these actors and AI off incase they are in view
// I origin them to make sure they are not potentialy stuck
// in the player at the end of the cinematic
void hideAlisonAndRusselIGM3()
{	
	$alison.ai_off();
	$alison.hide();
	$alison.notsolid();
	$alison.origin( '-9640 596 0' );
	$russel.ai_off();
	$russel.hide();
	$russel.notsolid();
	$russel.origin( '-9624 548 0' );
}

//Show these actors and AI on after cinematic and make patrol enve if conversation has
// not been triggered.
void sendAlisonAndRusselOnPatrolIGM3()
{	
	$triggerHallConversation1.thread( "" );	
	$alison.show();
	$alison.solid();
	$alison.resethead();
	$alison.ai_on();
	$russel.show();
	$russel.solid();
	$russel.resethead();
	$russel.ai_on();
}

///////////////////////////
// miyamotoOnUseWaitIGM3
// Wait for player to talk to actor
///////////////////////////
void miyamoto_workIGM3()
{
	setNextDialog( $miyamoto, 1 );
	setDialogFunction( $miyamoto, "miyamotoDialog" );
	while( 1 )
	{
		workUntilUsed( $miyamoto, "", "", 0 );
		talkUntilDone( $miyamoto );
	}
}

///////////////////////////
// miyamotoDialog3_1
// Player and actor talk
///////////////////////////
void miyamotoDialog3_1()
{
	$miyamoto.setFloatVar( "pauseRandomAnimate", 1 );
	wait( .25 );
	$miyamoto.anim( "ent-library-seat-scan" );
	$miyamoto.headwatch( $player, 10 );
	
	$miyamoto.playdialog("localization/sound/dialog/ent_m3/myamoto_helpyou.mp3", 1, 128, 0); //Can I help you?
	waitForDialog( $miyamoto );

	setNextDialog( $miyamoto, 2 );
	$miyamoto.resethead();
	setDialogFinished( $miyamoto );
	$miyamoto.setFloatVar( "pauseRandomAnimate", 0 );
}

///////////////////////////
// miyamotoDialog3_2
// Player and actor talk
///////////////////////////
void miyamotoDialog3_2()
{
	$miyamoto.setFloatVar( "pauseRandomAnimate", 1 );
	wait( .25 );
	$miyamoto.anim( "ent-library-seat-scan" );
	$miyamoto.headwatch( $player, 10 );

	$miyamoto.playdialog("localization/sound/dialog/ent_m3/myamoto_whatit.mp3", 1, 128, 0); //What is it?
	waitForDialog( $miyamoto );

	setNextDialog( $miyamoto, 3 );
	$miyamoto.resethead();
	setDialogFinished( $miyamoto );
	$miyamoto.setFloatVar( "pauseRandomAnimate", 0 );
}

///////////////////////////
// miyamotoDialog3_3
// Player and actor talk
///////////////////////////
void miyamotoDialog3_3()
{
	$miyamoto.setFloatVar( "pauseRandomAnimate", 1 );
	wait( .25 );
	$miyamoto.anim( "ent-library-seat-scan" );
	$miyamoto.headwatch( $player, 10 );
	
	$miyamoto.playdialog("localization/sound/dialog/ent_m3/myamoto_sorry.mp3", 1, 128, 0); //Sorry, I can't talk right now.
	waitForDialog( $miyamoto );

	setNextDialog( $miyamoto, 1 );
	$miyamoto.resethead();
	setDialogFinished( $miyamoto );
	$miyamoto.setFloatVar( "pauseRandomAnimate", 0 );
}

//===================================================================================================================
// Deck Change Check
//===================================================================================================================
//---------------------
// checkForDeckChangeIGM3	
//---------------------
void checkForDeckChangeIGM3()
{
	if( getfloatvar( "game.checkEntMission3RoomsVisited" ) == 2 )
	{
		setfloatvar(  "level.deckToChangeToIGM", 71 );

		$scriptmunro.playdialog("localization/sound/dialog/ent_m1/munro_dhazop2.mp3", 1, 10000, 0 ); //Hazard Ops.
	}
}

// --------------- TOOLTIPS

void libraryTerminal_tooltipNext( float page )
{globalCoop_player_transmitter(ePlayerResarching,"globalwidgetcommand ent_console_page"+page+"_toolip title $$ENT_CONSOLE_NEXTPAGE$$");}
void libraryTerminal_tooltipPrev( float page ){
globalCoop_player_transmitter( ePlayerResarching,"globalwidgetcommand ent_console_page"+page+"_toolip title $$ENT_CONSOLE_PREVPAGE$$");}

void libraryTerminal_tooltipNextPage1()	{libraryTerminal_tooltipNext(1);}
void libraryTerminal_tooltipNextPage2()	{libraryTerminal_tooltipNext(2);}
void libraryTerminal_tooltipNextPage3()	{libraryTerminal_tooltipNext(3);}
void libraryTerminal_tooltipNextPage4()	{libraryTerminal_tooltipNext(4);}
void libraryTerminal_tooltipNextPage5()	{libraryTerminal_tooltipNext(5);}
void libraryTerminal_tooltipExit()
{globalCoop_player_transmitter(ePlayerResarching,"globalwidgetcommand ent_console_page6_toolip title $$ENT_CONSOLE_EXIT$$");}

void libraryTerminal_tooltipPrevPage2()	{libraryTerminal_tooltipPrev(2);}
void libraryTerminal_tooltipPrevPage3()	{libraryTerminal_tooltipPrev(3);}
void libraryTerminal_tooltipPrevPage4()	{libraryTerminal_tooltipPrev(4);}
void libraryTerminal_tooltipPrevPage5()	{libraryTerminal_tooltipPrev(5);}
void libraryTerminal_tooltipPrevPage6()	{libraryTerminal_tooltipPrev(6);}


void entDeck11IGM3BranchingDialog_failsafe()
//___IMPORTANT___ NAME THIS FUNCTION AFTER THE NAME OF THE BRANCHDIALOG
//___EXAMPLE___ $telsia.branchdialog("entDeck11IGM3BranchingDialog");
//___NAME_IS_THEN___ entDeck11IGM3BranchingDialog
//___FULL_NAME_IS_THEN___ entDeck11IGM3BranchingDialog_failsafe
//___WARNING___ it is CASESENSITIVE: xxx_failsafe != xXx_FailSave
//
//HZM gameupdate chrissstrahl - this thread is called by the gamex86.dll
// if a the branchdialog is started with the name prefixed of _failsafe
//THIS IS A FAILSAFE thread, it makes sure that a option is selected upon a branch dialog
//otherwise a single player could brake the script or stall the game
//THIS NEEDS TO BE ADJUSTED FOR EVERY LEVEL-SCRIPT THAT CONTAINS A BRANCHDIALOG
{
	string sThread;
	//___IMPORTANT___ ADJUST THE NAME OF THE THREAD TO RUN HERE
	sThread = "entDeck8IGM3Option1";
	
	//start thread, this is the actual failsafe
	runThreadNamed(sThread);
}




