//geometry started in 200? by Luke Sutton
//script by chrissstrahl 20165.21.01 ulm, germany

float weaponsAllreadyRegistred = 0;
float cinematicLaptopSequence = 0;

void main();
void hideAndShowTimed(entity eHide);
void platesHide();
void gearUp();
void hallwayAttack();
void shuttleOps();
void shuttleBayGates();
void endLaptopCin();


//hzm coop mod - chrissstrahl - include the required coop level-scripts
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////
void	coop_endLevel();
entity	entityPlayerGlobal;

#include "coop_mod/matrix/main.scr"
#include "maps/global_scripts/global_common.scr"
/////////////////////////////////////////////////////////////////////////
/////////////////////////////////////////////////////////////////////////


void main()
{
	$symbol_shuttlebayTrigger1.scale(0.05);
	$symbolHazGearup0.scale(0.05);
	$symbolHazGearup1.scale(0.05);
	$hazLaptop.missionobjective(1);
	$hazLaptop.anim("open_idle");
	$laptopScreen0.hide();

//register weapon in the script inventory (players respawn with this)
	globalCoop_armory_weaponRegister("models/weapons/worldmodel-Phaser-stx.tik");

	//structural view mode
	$world.addAvailableViewMode("structuralintegrity");
	
	soundtrack( "music/dm_kw.mus" );
  
	//hzm coop mod chrissstrahl - author
	coop_string_levelAuthor = "Luke^0_^8Sutton";
	
	//hzm coop mod chrissstrahl - declare objectives - english
	coop_string_objectiveItem1 = "Get to the Hazard Ops to gear up.";
	coop_string_objectiveItem2 = "Repel the romulan invaders.";
	coop_string_objectiveItem3 = "Reach the Shuttle Bay.";
	coop_string_objectiveItem4 = "Open the Gates and take a Shuttle down to the Planet.";
	coop_string_objectiveItem5 = "Restore the Power to the Shuttle Bay Gates.";
	coop_string_objectiveTacticalInfo1 = "";//Use the distraction to your own advantage
	
	//hzm coop mod chrissstrahl - declare objectives - german
	coop_string_objectiveItem1_deu = "Schlagen Sie die Eindringle zur$$u$$ck.";
	coop_string_objectiveItem2_deu = "R$$u$$sten Sie sich aus in der Hazard Ops.";
	coop_string_objectiveItem3_deu = "Erreichen Sie die Shuttlerampe.";
	coop_string_objectiveItem4_deu = "$$O$$ffnen Sie die Tore und nehmen Sie ein Shuttle zur Oberfl$$a$$che.";
	coop_string_objectiveItem5_deu = "Stellen Sie die Energiezufur zu den Shuttlerampe Toren wieder her.";
	coop_string_objectiveTacticalInfo1_deu = "";//N$$u$$tzen Sie die Verwirrung zu Ihrem Vorteil
	
	coop_string_story = "Date: Jan 2388. USS Odyssey-A has been ordered to fight off an orbiting warbird, and liberate the planet. You and your teammates must battle through the streets of Attrexia and protect the population from Romulan invasion.";
	coop_string_story_deu = "Datum: Jan 2388. Die USS Odyssey-A wurde entsannt um einen Warbird zu vertreiben und den Planeten zu befreien. Sie und Ihr Team m$$u$$ssen sich durch die Stra$$S$$en von Attrexia k$$a$$mpfen und die Bev$$o$$lkerung vor den romulanischen Invsoren sch$$u$$tzen.";
	
	//Remove archetype
	//$t43.archetype("");

	waitforplayer();
	
//update objectives - repel, euip
	globalCoop_objectives_update("incomplete" , 1 , 0 );//gearup
	globalCoop_objectives_update("incomplete" , 2 , 1 );//repel
	
	//hzm coop mod chrissstrahl - Wait until the Game Starts in Multiplayer
	globalCoop_main_waitForWarmupTime();
	
	//hzm remove archetype
	/* //disabled 2018.03.22
	$consoleEnergy0.archetype("");
	$consoleEnergy1.archetype("");
	$consoleEnergy2.archetype("");
	$consoleHealth0.archetype("");
	$consoleHealth1.archetype("");
	$consoleHealth2.archetype("");
	$explosiveCrate0.archetype("");
	$explosiveCrate1.archetype("");
	$explosiveCrate2.archetype("");
	$explosiveCrate3.archetype("");
	*/
	
	//hzm coop mod chrissstrahl - show mod title - but before get the player out of all menus he currently has showing
	globalCoop_player_transmittToAll("popmenu 1;popmenu 0");//close 2 layers of menu
	wait(0.1);
	globalCoop_player_transmittToAll("popmenu 0");//close 1 layer of menu
	wait(0.1);
	thread globalCoop_player_transmittToAll("exec cfg/maps/coop_rotec/cTitle.cfg\n");
	globalCoop_player_transmittToAll("showmenu coop_cTitle\n");
	wait(10);
	globalCoop_player_transmittToAll("popmenu 1;popmenu 0");//close 2 layers of menu
	
	wait(2);
	
	//move all the plates in place, they are in a seperate room for lighting and shadow cast issues
	thread platesHide();
}

void hideAndShowTimed(entity eHide)
{
	eHide.hide();
	wait(5);
	eHide.show();
}

void laptopScreen1()
{
	cinematicLaptopSequence = 1;
	$laptopScreen0.hide();
	
	while(cinematicLaptopSequence == 1)
	{
		$laptopScreen1.show();
		wait(0.25);
		$laptopScreen1.hide();
		$laptopScreen2.show();
		wait(0.25);
		$laptopScreen2.hide();
		$laptopScreen3.show();
		wait(0.25);
		$laptopScreen3.hide();		
	}
}
void laptopScreen2()
{
	platesHide();
	cinematicLaptopSequence = 2;
	$laptopScreen6.show();
}
void laptopScreen3()
{
	platesHide();
	cinematicLaptopSequence = 3;
	$laptopScreen4.show();	
}
void laptopScreen4()
{
	platesHide();
	cinematicLaptopSequence = 4;
	$laptopScreen5.show();
}
void laptopScreen5()
{
	platesHide();
	cinematicLaptopSequence = 5;
	while(cinematicLaptopSequence == 5)
	{
		$laptopScreen1.show();
		wait(0.25);
		$laptopScreen1.hide();
		$laptopScreen2.show();
		wait(0.25);
		$laptopScreen2.hide();
		$laptopScreen3.show();
		wait(0.25);
		$laptopScreen3.hide();		
	}
}
void laptopScreen6()
{
	platesHide();
	cinematicLaptopSequence = 6;
	$laptopScreen9.show();
	wait(0.5);
	$laptopScreen9.hide();
	$laptopScreen8.show();
}
void laptopScreen7()
{
	platesHide();
	cinematicLaptopSequence = 7;
	while(cinematicLaptopSequence == 7)
	{
		$laptopScreen7.show();
		wait(0.25);
		$laptopScreen7.hide();
		$laptopScreen10.show();
		wait(0.25);
		$laptopScreen10.hide();
		$laptopScreen12.show();
		wait(0.25);
		$laptopScreen12.hide();
		$laptopScreen11.show();
		wait(0.25);		
		$laptopScreen11.hide();
	}
}
void platesHide()
{
	//kill currently running sequence
	killthread("laptopScreen"+cinematicLaptopSequence);
	
	//move all the plates in place, they are in a seperate room for lighting and shadow cast issues
	float i;
	entity ePlate;
	vector vOrigin;
	vOrigin = $laptopScreen0.getOrigin();
	for(i=0;i<13;i++){
		ePlate = getEntity("laptopScreen" + i);
		ePlate.origin(vOrigin);
		ePlate.hide();
	}
}

void useLaptop()
{
	$hazLaptop.missionobjective(0);
	$hazGearupTrigger0.missionobjective(1);
	$hazGearupTrigger1.missionobjective(1);
	
	$hazGearupTrigger0.triggerable();
	$hazGearupTrigger1.triggerable();
	
	$symbolHazGearup0.scale(1);
	$symbolHazGearup1.scale(1);
	
	/*
	spawn("camera","targetname","cam1");
//paint it black
	cam_fadeout( 1 , 0, 0, 0, 1, 0 );
	wait(1);

	start the actual cinematic
	cinematic();
	freezeplayer();
	cuecamera($cam1);
	letterbox( .1 );
	$cam1.cut();
	$cam1.follow($cinematicLaptop);
	wait(0.2);
	remove black
	cam_fadeIn( 1 , 0, 0, 0, 0, 0 );
	*/
	
	$hazLaptop.playsound("sound/coop_rotec/rotech_video_brefing.mp3",0,2,512);
	
	//remove and add the plates depending on the sound file status
	wait(1);
	$laptopScreen0.show();//incomming
	
	skipthread("endLaptopCin");
	
	wait(4);
	thread laptopScreen1();//luke talk
	wait(19);
	thread laptopScreen2();//leader
	wait(10);
	thread laptopScreen3();//planet
	wait(8);
	thread laptopScreen4();//civilian
	wait(12);
	thread laptopScreen5();//luke talk
	//wait(17);
	wait(30);
	thread laptopScreen6();//attack
	wait(10);
	thread laptopScreen7();//luke talk emotional
	wait(10);
	thread endLaptopCin();
}

void endLaptopCin()
{
	//skipthread("");
	killthread("useLaptop");
	$hazLaptop.stopsound(0);
	
/*
//paint it black
	cam_fadeout( 1 , 0, 0, 0, 1, 0 );
	wait(1);
	noncinematic();
	clearletterbox( .1 );
	releaseplayer();	
	cueplayer();
	wait(1);
//remove black
	cam_fadeIn( 1 , 0, 0, 0, 0, 0 );
	
	$hazGearupTrigger0.triggerable();
	$hazGearupTrigger1.triggerable();
	
	$symbolHazGearup0.scale(1);
	$symbolHazGearup1.scale(1);
*/
	platesHide();
}

void gearUp()
{
	entity eTrigger;
	entity ePlayer;
	entity eWeapon1;
	entity eWeapon2;
	entity eWeapon3;
	entity eBoots;
	entity ePants;
	entity eShirt;
	entity eGear;
	
	eTrigger = getCurrentEntity();
	ePlayer = eTrigger.getLastActivatingEntity();
	
	float fStart,fCurrent;
	fStart		= eTrigger.getFloatVar("uservar1");
	fCurrent	= eTrigger.getFloatVar("current");

	if(fCurrent == 0){
		fCurrent += fStart;
	}
	
	//empty, wait for it to refill
	if(fCurrent >= (fStart + 3)){	
		ePlayer.hudprint("$$Empty$$\n");
		return;
	}
	
	if( ! weaponsAllreadyRegistred ){
		 weaponsAllreadyRegistred=1;
		
		globalCoop_armory_weaponRegister("models/weapons/worldmodel-Tricorder-stx.tik");
		globalCoop_armory_weaponRegister("models/weapons/worldmodel-BurstRifle.tik");
		globalCoop_armory_weaponRegister("models/weapons/worldmodel-CompressionRifle.tik");
		
		//set marker on the radar
		$hazGearupTrigger0.missionobjective(0);
		$hazGearupTrigger1.missionobjective(0);
		$hazteam0.missionobjective(1);
		
		//update objectives - euip
		thread globalCoop_objectives_update("complete" , 1 , 0 );//gear up
		thread globalCoop_objectives_update("incomplete" , 3 , 1 );//reach shuttlebay
		
		//open door and keep it open for a while, signaling the player where to go
		$hazDoor2.unlock();
		$hazDoor2.open($world);
		
		//make em attack
		thread hallwayAttack();
		
		//was turned off to avoid premature detection of the player
		$hallwayInvader2.ai_on();
	}
	
	eWeapon1 = getEntity("hazburst" + fCurrent);
	eWeapon2 = getEntity("hazComp" + fCurrent);
	eWeapon3 = getEntity("hazTricorder" + fCurrent);
	eBoots = getEntity("hazBoots" + fCurrent);
	ePants = getEntity("hazPants" + fCurrent);
	eShirt = getEntity("hazShirt" + fCurrent);
	eGear = getEntity("hazGear" + fCurrent);
	
	fCurrent++;
	eTrigger.setFloatVar("current",fCurrent);
	
	ePlayer.turntowardsEntity(eShirt);
	wait(0.1);
	
	eShirt.hide();
	ePants.hide();
	eGear.hide();
	eBoots.hide();
	wait(0.25);
	
	eWeapon1.hide();
	eWeapon2.hide();
	eWeapon3.hide();
	
	//hzm give weapon now to players
	globalCoop_armory_equip(ePlayer);
	
	if(fCurrent == (fStart + 3)){	
		wait(5);
		for(fCurrent=fStart;fCurrent<(fStart + 3);fCurrent++){
			eWeapon1 = getEntity("hazburst" + fCurrent);
			eWeapon2 = getEntity("hazComp" + fCurrent);
			eWeapon3 = getEntity("hazTricorder" + fCurrent);
			eBoots = getEntity("hazBoots" + fCurrent);
			ePants = getEntity("hazPants" + fCurrent);
			eShirt = getEntity("hazShirt" + fCurrent);
			eGear = getEntity("hazGear" + fCurrent);
			
			eShirt.show();
			eShirt.displayeffect("TransportIn","FederationWeapons");
			ePants.show();
			ePants.displayeffect("TransportIn","FederationWeapons");
			eGear.show();
			eGear.displayeffect("TransportIn","FederationWeapons");
			eBoots.show();
			eBoots.displayeffect("TransportIn","FederationWeapons");
			
			wait(0.2);
			
			eWeapon1.show();
			eWeapon1.displayeffect("TransportIn","FederationWeapons");
			eWeapon2.show();
			eWeapon2.displayeffect("TransportIn","FederationWeapons");
			eWeapon3.show();
			eWeapon3.displayeffect("TransportIn","FederationWeapons");
			
			wait(1);
		}
		eTrigger.setFloatVar("current",fStart);
		eTrigger.triggerable();
	}
}

void hallwayAttack()
{
	$hallwayInvader0.aggressive(1);
	$hallwayInvader1.attack(globalCoop_return_playerClosestPreferActive($hallwayInvader1));
	$hallwayInvader1.aggressive(1);
	$hallwayInvader2.aggressive(1);
	$hallwayInvader4.aggressive(1);
	
	$hallwayInvader2.show();
	$hallwayInvader4.show();
	
	$hallwayInvader2.solid();
	$hallwayInvader4.solid();
	$hallwayInvader4.ai_on();
}
void hallwayAttackSneaky()
{
	$hallwayInvader3.show();
	$hallwayInvader3.solid();
	$hallwayInvader3.ai_on();
	$hallwayInvader3.aggressive(1);
}

void shuttleOps()
{
	$hazteam0.missionobjective(0);
	$trigger_cargoBayGates.missionobjective(1);

	//battle sequence, activate the ai
	
	//we don't want him to follow us yet!
	$hazardTeam1.settendency("follow",0);
	$hazardTeam1.ai_on();
	
	//let the battle commence
	$hazteam0.ai_on();
	$hazteam0.settendency("follow",0);
	$hazteam1.ai_on();
	$hazteam1.settendency("follow",0);
	$hazteam2.ai_on();
	$hazteam2.settendency("follow",0);
	$hazteam3.ai_on();
	$hazteam3.settendency("follow",0);
	
	//ROMULAN AI ???
	//ROMULAN AI ???
	//ROMULAN AI ???
	//ROMULAN AI ???
	
	float i;
	entity e;
	for(i=0;i<11;i++){
		e = getEntity("invader" + i);
		if(doesEntityExist( e )){
			if(i < 8){
				e.ai_on();
				if( i == 4 && globalCoop_return_integerPlayersQuantity(2) < 2){
					e.remove();
				}
			}
			else{
				if( globalCoop_return_integerPlayersQuantity(2) > 2 || i == 10 ){//keep sniper allways
					e.ai_on();
					e.solid();
					e.show();
				}else{
					e.remove();
				}
				if( globalCoop_return_integerPlayersQuantity(2) > 3 ){
					e.ai_on();
					e.solid();
					e.show();
				}
				else{
					e.remove();
				}
			}
		}
	}
}

void shuttleBayGates()
{
	//start objective for gates to open/reroute power
	$hazardTeam1.settendency("follow",1);
	
	$shuttlebayTrigger0.triggerable();
	$shuttlebayTrigger1.triggerable();
	$shuttlebayTrigger2.triggerable();
	

	$trigger_cargoBayGates.missionobjective(0);
	$shuttlebayTrigger1.missionobjective(1);
	$symbol_shuttlebayTrigger1.scale(1);
	
	$shuttlebayBigGate.open($world);
	$shuttlesFlyOver.triggerable();
	
	float i;
	entity e;
	for(i=0;i<5;i++){
		e = getEntity("invaderExtraWave" + i);
		if(doesEntityExist( e )){
			e.show();
			e.solid();
			e.displayEffect("TransportIn","Romulan");
			wait(0.25);
			e.ai_on();
			e.aggressive(1);
		}
	}
	
}

void shuttlesFlyOver()
{
	entity eTrigger;
	entity ePlayer;
	$skyRomshuttle.bind($skyShuttles_origin);
	$skyFedshuttle0.bind($skyShuttles_origin);
	$skyFedshuttle1.bind($skyShuttles_origin);
	$skyShuttles_origin.moveUp(25);
	$skyShuttles_origin.moveForward(130);
	$skyShuttles_origin.time(7);
}

void shuttleBayEntred()
{
	vector v;
	v = $trigger_endMission.getOrigin();
	v_z += 150;
	spawn("Object","model","sysimg/icons/mp/team_blue.spr","scale","0.5","origin",""+v,"targetname","pointerBlue");
	$shuttlebayTrigger1.missionobjective(0);
	wait(0.05);
	//$endMission.missionobjective(1);
	//$pointerBlue.rendereffects("+depthhack");//does not work on sprites
	$pointerBlue.missionobjective(1);
}

void endMission()
{
	spawn("camera","targetname","cam2");
//paint it black
	cam_fadeout( 1 , 0, 0, 0, 1, 0 );
	wait(1);

	$hazardTeam1.remove();//teammate with you in shuttle
	$hazteam2.remove();//might wander into shuttle path, we can't have that
	
	//start the actual cinematic
	cinematic();
	freezeplayer();
	cuecamera($cam2);
	letterbox( .1 );
	$cam2.cut();
	$cam2.follow($cinematicShuttleTakeoff);
	wait(0.2);
	//remove black
	cam_fadeIn( 1 , 0, 0, 0, 0, 0 );
	
	$shuttleTakeoff.bind($shuttleTakeoff_origin);
	$shuttleTakeoff_origin.followpath($shuttleTakeoff_path);
	wait(7);
	
//paint it black
	cam_fadeout( 1 , 0, 0, 0, 1, 0 );
	wait(1);
	noncinematic();
	clearletterbox( .1 );
	wait(1);	
	thread coop_endLevel();
}

void coop_endLevel()
//------------------------------------------------------------------------------
//Level end
//------------------------------------------------------------------------------
{
//mark this map as completed to the scripts (can not fail anymore)
	//globalCoop_mission_accomplished();
//credits, and history of map
	//globalCoop_main_globalCommand("nodamage");
	//globalCoop_huds_AddAll("coop_rotec_credits");
	//wait(10);
//load next map
	thread globalCoop_mission_completed("coop_rotec2");
}



