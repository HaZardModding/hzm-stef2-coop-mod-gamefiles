//created 2021.11.14 chrissstrahl, Germany, Ulm
//
//Cygnus Example Script
//


//================================
//DECLARATIONS - makes sure the functions can be called in random order
//================================
void main();
void populateSymbolVars();
void setupLevel();
void setupArchetypes();
void setupSymbols();
void setupSymbolsWalkHere(string stargetname,float fGreen);
void spawnSymbolAt(string sSymbol, float fScale, float fVerticalOffset,entity eLocation,string sTargetname);
void replaceSymbol(string sSymbol, float fScale, float fVerticalOffset,entity eLocation,string sTargetname);
entity spawnSymbol(string sSymbol, float fScale, float fVerticalOffset,vector vLocation,string sTargetname);
void puzzleSolved();
void puzzleAreaScanned();
void puzzleSolvedFederation();

//================================
//GLOBAL-LEVEL VARIABLES - every script function can accsess them
//================================
string SYMBOL_ARROW_RED;
string SYMBOL_ARROW_BLUE;
string SYMBOL_ARMOR;
string SYMBOL_TRANSPORTER;
string SYMBOL_BOOTS;
string SYMBOL_BOOTS2;
string SYMBOL_GLOVE;
string SYMBOL_STAR;
string SYMBOL_STAR2;
string SYMBOL_BOMB2;
string SYMBOL_HYPO;
string SYMBOL_MEDKIT;
string SYMBOL_EXPLOSIVE;
string SYMBOL_TARGET;
string SYMBOL_TARGET2;
string SYMBOL_DEATH;
string SYMBOL_SNIPER;
string SYMBOL_AMMO_F;
string SYMBOL_AMMO_P;
string SYMBOL_AMMO_I;
string SYMBOL_MEDIC;
string SYMBOL_TONGS;
string SYMBOL_PAD;
string SYMBOL_IMOD;
string SYMBOL_FED;
string SYMBOL_BADGE;
string SYMBOL_BOMB;


//================================
//INCLUDES - load additional functionality from other scripts
//================================
#include "maps/global_scripts/global_common.scr"
#include "maps/global_scripts/global_archetype.scr"



//================================
//This function is started by the game after the level is loaded completly
//================================
void main()
{
	//start the setup for the level
	//-to keep main function lightweight this is in a seperate function
	populateSymbolVars();
	setupLevel();
}

//================================
//set values to symbol variables - needs to be done seperate because strings can not be inizialised with values :'(
//================================
void populateSymbolVars()
{
	SYMBOL_ARROW_RED="sysimg/icons/mp/team_red.spr";
	SYMBOL_ARROW_BLUE="sysimg/icons/mp/team_blue.spr";
	SYMBOL_ARMOR="sysimg/icons/items/armor.spr";
	SYMBOL_ARMOR="sysimg/icons/items/armor.spr";
	SYMBOL_TRANSPORTER="sysimg/icons/items/holdableitem_transporter.spr";
	SYMBOL_BOOTS="sysimg/icons/items/powerup_speed.spr";
	SYMBOL_BOOTS2="sysimg/icons/mp/specialty_infiltrator.spr";
	SYMBOL_GLOVE="sysimg/icons/items/powerup_strength.spr";
	SYMBOL_STAR="sysimg/icons/mp/actionhero_icon.spr";
	SYMBOL_STAR2="sysimg/icons/mp/specialty_heavyweapons.spr";
	SYMBOL_BOMB2="sysimg/icons/mp/specialty_demolitionist.spr";
	SYMBOL_HYPO="sysimg/icons/items/health.spr";
	SYMBOL_MEDKIT="sysimg/icons/items/holdableitem_medkit.spr";
	SYMBOL_EXPLOSIVE="sysimg/icons/items/holdableitem_explosive.spr";
	SYMBOL_TARGET="sysimg/icons/items/rune_armorpiercing.spr";
	SYMBOL_TARGET2="sysimg/icons/mp/specialty_sniper.spr";
	SYMBOL_DEATH="sysimg/icons/items/rune_deathquad.spr";
	SYMBOL_SNIPER="sysimg/icons/items/wep_sniper.spr";
	SYMBOL_AMMO_F="sysimg/icons/items/ammo_fed.spr";
	SYMBOL_AMMO_P="sysimg/icons/items/ammo_idryll.spr";
	SYMBOL_AMMO_I="sysimg/icons/items/ammo_plasma.spr";
	SYMBOL_TONGS="sysimg/icons/mp/specialty_technician.spr";
	SYMBOL_PAD="sysimg/icons/mp/talking.spr";
	SYMBOL_MEDIC="sysimg/icons/mp/specialty_medic.spr";
	SYMBOL_IMOD="sysimg/icons/items/wep_imod.spr";
	SYMBOL_BADGE="sysimg/icons/mp/award_firstStike.spr";
	SYMBOL_FED="sysimg/icons/mp/award_impressive.spr";
	SYMBOL_BOMB="sysimg/icons/mp/diffusion_bomber.spr";
}

//================================
//do stuff here that needs to be done on level start
//================================
void setupLevel()
{
	setupArchetypes();
	setupSymbols();
}

//================================
//execute functions to convert script_object brush to Archetype Models
//================================
//Interactive Types: Usable, TricorderUsable, Destructable, Location, MissionObjective, Teammate, Friendly, Enemy
void setupArchetypes()
{
	//enterprise console
	globalArchetype_Setup ( $archetypeConsoleLeft , "Usable" );
	globalArchetype_Setup ( $archetypeConsoleCenter , "Usable" );
	globalArchetype_Setup ( $archetypeConsoleRight , "Usable" );
	
	//Tricorder Platform Area
	globalArchetype_Setup ( $archetypeScan0 , "TricorderUsable" );
}

//================================
//execute functions to PLACE and REPLACE symbold
//================================
//For a list of symbols see: populateSymbolVars() above
void setupSymbols()
{
	//spawn symbol at triggers, and keep trigger
	spawnSymbolAt(SYMBOL_GLOVE, 0.5, 0,$triggerdoort3,"");
	spawnSymbolAt(SYMBOL_GLOVE, 0.15, 7,$archetypeConsoleLeft,"");
	spawnSymbolAt("coop_use.spr", 0.3,16,$archetypeConsoleCenter,"");
	spawnSymbolAt("coop_modulate.spr", 1, 30,$puzzleArchetypeScan0,"giantTricorderSymbol");
	
	//spawn/replace symbol at entity, and remove entity
	replaceSymbol(SYMBOL_GLOVE, 0.15, 0,$usesymbol0,"");
	replaceSymbol("coop_modulate.spr", 0.5, 0,$tricordersymbol0,"");
	replaceSymbol(SYMBOL_GLOVE, 0.15, 0,$usesymbol0,"");
	
	//show allmost all symbols there are - Middle row - using the above set variables
	spawnSymbolAt(SYMBOL_ARROW_RED, 0.15, 10,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_ARROW_BLUE, 0.15, 20,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_ARMOR, 0.15, 30,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_TRANSPORTER, 0.15, 40,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_BOOTS, 0.15, 50,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_GLOVE, 0.15, 60,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_STAR, 0.09, 70,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_BOMB, 0.15, 80,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_FED, 0.15, 90,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_IMOD, 0.15, 100,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_BADGE, 0.15, 110,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_HYPO, 0.15, 120,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_MEDKIT, 0.15, 130,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_TARGET, 0.15, 140,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_EXPLOSIVE, 0.15, 150,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_DEATH, 0.15, 160,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_SNIPER, 0.15, 170,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_AMMO_F, 0.15, 180,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_AMMO_P, 0.15, 190,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_AMMO_I, 0.15, 200,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_TONGS, 0.15, 210,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_TARGET2, 0.15, 220,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_BOMB2, 0.15, 230,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_STAR2, 0.15, 240,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_MEDIC, 0.15, 250,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_PAD, 0.15, 260,$placeAllSymbolsHere,"");
	spawnSymbolAt(SYMBOL_BOOTS2, 0.15, 270,$placeAllSymbolsHere,"");
	
	//left row
	spawnSymbolAt("sysimg/icons/mp/cp_alpha_neutral.spr", 0.15, 10,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/cp_alpha_blue.spr", 0.15, 20,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/cp_alpha_red.spr", 0.15, 30,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/cp_beta_neutral.spr", 0.15, 40,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/cp_gamma_neutral.spr", 0.15, 50,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/cp_delta_neutral.spr", 0.15, 60,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/ctf_flagcarrier.spr", 0.15, 70,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/ctf_oneFlagTaken.spr", 0.15, 80,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/ctf_oneFlagMissing.spr", 0.15, 90,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/ctf_oneFlagInBase.spr", 0.15, 100,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/ctf_blueFlagTaken.spr", 0.15, 110,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/ctf_blueFlagMissing.spr", 0.15, 120,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/ctf_blueFlagInBase.spr", 0.15, 130,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/diffusion_bombOnground.spr", 0.15, 140,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/diffusion_bombArmed-red.spr", 0.15, 150,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/diffusion_bomber.spr", 0.15, 160,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/award_denied.spr", 0.15, 170,$placeAllSymbolsHere2,"");
	spawnSymbolAt("sysimg/icons/mp/award_excellent.spr", 0.15, 180,$placeAllSymbolsHere2,"");
	
	//right row - using Coop Mod Symbols Shader
	spawnSymbolAt("coop_use.spr", 0.15, 10,$placeAllSymbolsHere3,"");
	spawnSymbolAt("coop_modulate.spr", 0.15, 20,$placeAllSymbolsHere3,"");
	spawnSymbolAt("coop_destroy.spr", 0.15, 30,$placeAllSymbolsHere3,"");
	spawnSymbolAt("coop_bomb.spr", 0.15, 40,$placeAllSymbolsHere3,"");

	//replace marker entities for walk here symbol
	setupSymbolsWalkHere("walkhere",1); //green boots
	setupSymbolsWalkHere("walknothere",0); //lilac boots
}

//================================
//executed when door puzzle is solved
//================================
void puzzleSolved()
{
	//unloack and open door
	$doorPuzzleLocked.unlock();
	$doorPuzzleLocked.open($world);
	$tricordersymbol0.remove();
}

//================================
//executed when area puzzle is solved
//================================
void puzzleAreaScanned()
{
	//remove archetype
	//remove tricorder symbol
	$archetypeScan0.remove();
	$giantTricorderSymbol.remove();
}

//================================
//executed when area puzzle is solved
//================================
void puzzleSolvedFederation()
{
	$symbolmodulatefederation.remove();
	wait(0.05);
}

//================================
//grab continues named entities and replace them with symbols
//================================
void setupSymbolsWalkHere(string sTargetname,float fGreen)
{
	//continue grabbing and replacing as long as the next higher number in the targetname is present
	//starts at 0 continues with 1 2 3 4 ...
	//limit is 50
	float fNumber;
	float fLimit=50;
	entity eReplaceMe;
	string sSprite;
	
	sSprite=SYMBOL_BOOTS;
	if(fGreen){
		sSprite=SYMBOL_BOOTS2;
	}
	for(fNumber=0;fNumber<fLimit;fNumber++){
		eReplaceMe = getEntity(sTargetname+""+fNumber);
		cprint("setupSymbolsWalkHere "+sTargetname+""+fNumber+"\n");
		print("setupSymbolsWalkHere "+sTargetname+""+fNumber+"\n");
		
		if(doesEntityExist(eReplaceMe)){
			replaceSymbol(sSprite, 0.15, 0,eReplaceMe,"");
			cprint("setupSymbolsWalkHere"+fNumber+"\n");
		}else{
			cprint("setupSymbolsWalkHere limit\n");
			print("setupSymbolsWalkHere limit\n");
			fNumber = fLimit;
		}
	}
}

void spawnSymbolAt(string sSymbol, float fScale, float fVerticalOffset,entity eLocation,string sTargetname)
//================================
//makes the given symbol spawn at the given entity
//================================
{
	//print error message and exit function
	//print error to screen, print error to console
	//exit function
	if(!doesEntityExist(eLocation)){
		centerprint("ERROR: (spawnSymbolAt) Entity "+eLocation.getTargetname()+" does not exist!\n");
		print("ERROR: (spawnSymbolAt) Entity "+eLocation.getTargetname()+" does not exist!\n");
		return;
	}
	
	//execute symbol spawn function
	//wait for it to spawn
	//bind symbol to entity, so if entity moves symbol goes with it
	entity eSymbol;
	eSymbol = spawnSymbol(sSymbol,fScale,fVerticalOffset,eLocation.getOrigin(),sTargetname);
	eSymbol.bind(eLocation);
	//show entity, because triggers are hidden by default and when bound symbol hides as well
	eLocation.show();
}

void replaceSymbol(string sSymbol, float fScale, float fVerticalOffset,entity eLocation,string sTargetname)
//================================
//makes the given symbol spawn at the given entity and remove the given entity
//================================
{
	//print error message and exit function
	//print error to screen, print error to console
	//exit function
	if(!doesEntityExist(eLocation)){
		centerprint("ERROR: (replaceSymbol) Entity "+eLocation.getTargetname()+" does not exist!\n");
		print("ERROR: (replaceSymbol) Entity "+eLocation.getTargetname()+" does not exist!\n");
		return;
	}
	//execute symbol spawn function
	//grab location where to spawn, this allowes us to remove the given entity before spawning a new one
	//remove given entity
	//wait for it to spawn
	//bind symbol to entity, so if entity moves symbol goes with it
	entity eSymbol;
	vector vLocation;
	vLocation = eLocation.getOrigin();
	eLocation.remove();
	spawnSymbol(sSymbol,fScale,fVerticalOffset,vLocation,sTargetname);
}

//================================
//makes the given symbol spawn at the given entity
//================================
entity spawnSymbol(string sSymbol, float fScale, float fVerticalOffset,vector vLocation,string sTargetname)
{
	entity eSymbol;
	vector v;
	v = vLocation;
	v_z += fVerticalOffset;
	eSymbol = spawn("Object","model",sSymbol,"origin",""+v,"targetname",sTargetname,"scale",""+fScale);
	wait(0.05);
	return eSymbol;
}





