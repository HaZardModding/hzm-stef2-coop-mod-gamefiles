//setup Dialog Options for Barclay
//----------------------------------------------------
void usedBarclay();

//Dialog Options selected for Barclay
//----------------------------------------------------
void barclayOption1();
void barclayOption2();
void barclayOption3();
void barclayOption4();

void usedBarclay()
//Barclay was used by player
{
	//----------------------------------------------------
	// clear onUse command handle
	//----------------------------------------------------
	entity eTrigger;
	eTrigger = getCurrentEntity();
	if(doesEntityExist(eTrigger)){
		eTrigger.noUse();
	}else{
		thread printConsole("usedBarclay failed");
	}
	
	//----------------------------------------------------
	// Grab closest player - assume they used barclay
	//----------------------------------------------------
	entity ePlayer;
	ePlayer = playerReturnClosest($barclay);
	if(!doesEntityExist(ePlayer)){
		thread printConsole("usedBarclay could not grab any player at all - Terminating");
		return;
	}
	
	//----------------------------------------------------
	//if every option on barclay was used at least once - exit
	//----------------------------------------------------
	if(	countGet($barclay,"dialogUsedOption1") != 0	&&
		countGet($barclay,"dialogUsedOption2") != 0	&&
		countGet($barclay,"dialogUsedOption3") != 0	&&
		countGet($barclay,"dialogUsedOption4") != 0	)
	{
		messagePlayer(ePlayer,"Barclay is done talking to you!");
		
		//----------------------------------------------------
		// Allow to use Barclay again
		//----------------------------------------------------
		wait(2);
		thread onUseAction($barclay,"usedBarclay");
		
		return;
	}

	//----------------------------------------------------
	// make watch player, slow turning head
	//----------------------------------------------------
	$barclay.headwatch(ePlayer,3.5);

	//----------------------------------------------------
	// Start/Open Circle Menu (2 = Dialog Circle Menu) and set options
	//----------------------------------------------------
	ePlayer.circleMenu(2);
	
	//INFO
	//$player.circleMenuDialogSet(float optionNumber,string text,string threadName, string imageORshader);
	
	//----------------------------------------------------
	// Set or Clear Options for Dialog Circle Menu
	// based on if a option was used before
	//----------------------------------------------------
	if( countGet($barclay,"dialogUsedOption1") > 0 ){
		ePlayer.circleMenuClear(1);
	}else{
		ePlayer.circleMenuDialogSet(1,"What is the most sexy thing about a Ship ?","barclayOption1","sysimg/icons/mp/award_denied.tga");
	}
	
	if( countGet($barclay,"dialogUsedOption2") > 0 ){
		ePlayer.circleMenuClear(2);
	}else{
		ePlayer.circleMenuDialogSet(2,"How can I help you ?.","barclayOption2","sysimg/icons/mp/award_champion.tga");
	}
	
	if( countGet($barclay,"dialogUsedOption3") > 0 ){
		ePlayer.circleMenuClear(3);
	}else{
		ePlayer.circleMenuDialogSet(3,"Option 3","barclayOption3","sysimg/icons/mp/diffusion_bombOnground.tga");
	}
	
	if( countGet($barclay,"dialogUsedOption4") > 0 ){
		ePlayer.circleMenuClear(4);
	}else{
		ePlayer.circleMenuDialogSet(4,"What do you say if somone comes late ?","barclayOption4","sysimg/icons/mp/diffusion_bombArmed-red.tga");
	}
}

void barclayOption1()
//Barclay Dialog Option 1 was clicked
{
	//----------------------------------------------------
	// Grab player - This is executed by player circle menu not via trigger!
	//----------------------------------------------------
	entity ePlayer;
	ePlayer = getCurrentEntity();
	
	//----------------------------------------------------
	// Remember to disable Option 1
	//----------------------------------------------------
	countUp($barclay,"dialogUsedOption1");

	//----------------------------------------------------
	// Show Message to player
	//----------------------------------------------------
	messagePlayer(ePlayer,"Barclay Option 1 selected\n");	
	
	//----------------------------------------------------
	// show some kind of animation
	//----------------------------------------------------
	$barclay.animateonce("cin-m3_sitting");
	waitForAnimation($barclay,"cin-m3_sitting",0.15);	
	
	//----------------------------------------------------
	// reset head
	//----------------------------------------------------
	$barclay.resethead(5);
	
	//----------------------------------------------------
	// Allow to use Barclay again
	//----------------------------------------------------
	thread onUseAction($barclay,"usedBarclay");
}

void barclayOption2()
//Barclay Dialog Option 2 was clicked
{
	//----------------------------------------------------
	// Grab player - This is executed by player circle menu not via trigger!
	//----------------------------------------------------
	entity ePlayer;
	ePlayer = getCurrentEntity();

	//----------------------------------------------------
	// Show Message to player
	//----------------------------------------------------
	messagePlayer(ePlayer,"Barclay Option 2 selected\n");
	
	//----------------------------------------------------
	// Remember to disable Option 2
	//----------------------------------------------------
	countUp($barclay,"dialogUsedOption2");
	
	//----------------------------------------------------
	// reset head
	//----------------------------------------------------
	$barclay.resethead(5);
	
	//----------------------------------------------------
	// show some kind of animation
	//----------------------------------------------------
	$barclay.animateonce("conv-handflick-hellno");
	waitForAnimation($barclay,"conv-handflick-hellno",0.15);
	
	//----------------------------------------------------
	// Allow to use Barclay again
	//----------------------------------------------------
	thread onUseAction($barclay,"usedBarclay");
}

void barclayOption3()
//Barclay Dialog Option 3 was clicked
{
	//----------------------------------------------------
	// Grab player - This is executed by player circle menu not via trigger!
	//----------------------------------------------------
	entity ePlayer;
	ePlayer = getCurrentEntity();
	
	//----------------------------------------------------
	// Show Message to player
	//----------------------------------------------------
	messagePlayer(ePlayer,"Barclay Option 3 selected\n");
	
	//----------------------------------------------------
	// Remember to disable Option 3
	//----------------------------------------------------
	countUp($barclay,"dialogUsedOption3");
	
	//----------------------------------------------------
	// show some kind of animation
	//----------------------------------------------------
	$barclay.animateonce("conv-gesture1");
	waitForAnimation($barclay,"conv-gesture1",0.15);
	
	//----------------------------------------------------
	// reset head
	//----------------------------------------------------
	$barclay.resethead(5);
	
	//----------------------------------------------------
	// Allow to use Barclay again
	//----------------------------------------------------
	thread onUseAction($barclay,"usedBarclay");
}

void barclayOption4()
//Barclay Dialog Option 4 was clicked
{
	//----------------------------------------------------
	// Grab player - This is executed by player circle menu not via trigger!
	//----------------------------------------------------
	entity ePlayer;
	ePlayer = getCurrentEntity();

	//----------------------------------------------------
	// Show Message to player
	//----------------------------------------------------
	messagePlayer(ePlayer,"Barclay Option 4 selected\n");
	
	//----------------------------------------------------
	// Remember to disable Option 4
	//----------------------------------------------------
	countUp($barclay,"dialogUsedOption4");
	
	//----------------------------------------------------
	// Activate Enemy Klingons
	//----------------------------------------------------
	$klingon1.health(1);
	$klingon1.show();
	$klingon1.solid();
	$klingon1.ai_on();
	
	$barclay.resethead(5);
	
	//----------------------------------------------------
	// show some kind of animation
	//----------------------------------------------------
	$barclay.animateonce("conv-fingerup-warning");
	waitForAnimation($barclay,"conv-fingerup-warning",0.15);
	
	//----------------------------------------------------
	// Activate Barclay
	//----------------------------------------------------
	$barclay.health(1000);
	$barclay.ai_on();
	$barclay.attack($klingon1);
	
	wait(5);
	$klingon1.kill();
	$barclay.ai_off();
	
	//----------------------------------------------------
	// Make barclay go back and wait until barclay has stoped moving
	//----------------------------------------------------
	//npcWalkTo($barclay,$barclayReturnPlace,"run"); - this does not work with nodes, only with other entities
	npcWalkToNode($barclay,"$barclayReturnPlace","run");
	
	//----------------------------------------------------
	// Allow to use Barclay again
	//----------------------------------------------------
	thread onUseAction($barclay,"usedBarclay");
}
