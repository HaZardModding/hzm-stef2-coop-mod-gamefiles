entity	returnPlayerFromTrigger(entity eTrigger);
void	printMsg(entity ePlayer,string sMessage);
void	entryDescription();

void	main();
void	currentRank();
void	promoteRank();
void	demoteRank();
void	saveRank();
void	retriveRank();
void	compareRank();

void	coop_endLevel();
#include "coop_mod/matrix/main.scr"
#include "coop_mod/matrix/rank.scr"


entity returnPlayerFromTrigger(entity eTrigger)
//----------------------------------------------------------------------
// helper function
//----------------------------------------------------------------------
{
	if(doesEntityExist(eTrigger)){
		entity ePlayer;
		ePlayer = eTrigger.getLastActivatingEntity();
		if(doesEntityExist(ePlayer)){
			return ePlayer;
		}
	}
	entity eEmpty;
	return eEmpty;
}

void printMsg(entity ePlayer,string sMessage)
//----------------------------------------------------------------------
// helper function
//----------------------------------------------------------------------
{
	if(getCvarInt("g_gametype") == 0){
		centerprint(sMessage+"\n");
	}
	else{
		entity ePlayer;
		ePlayer.hudPrint(sMessage+"\n");
	}
}

void entryDescription()
//----------------------------------------------------------------------
// prints the text from the trigger to player
//----------------------------------------------------------------------
{
	entity ePlayer;
	entity eTrigger;
	eTrigger = getCurrentEntity();
	ePlayer = eTrigger.getLastActivatingEntity();
	if(doesEntityExist(ePlayer)){
		string sMessage;
		sMessage = eTrigger.getStringVar("uservar1");
		printMsg(ePlayer,"^2Info: "+sMessage);
	}else{
		printMsg($player0,"^1Info: Could not retive last activating entity");
	}
}

void main()
//----------------------------------------------------------------------
// started by the game on level start - 
//----------------------------------------------------------------------
{
	//add ranks, do not add as thread, the order defines priority
	rank_add("Enemy");
	rank_add("Civilian");
	rank_add("Citizen");
	rank_add("Crewman");
	rank_add("Ensign");
	rank_add("Lt");
	rank_add("Lt CMD");
}

void currentRank()
//----------------------------------------------------------------------
// shows your current rank
//----------------------------------------------------------------------
{
	entity ePlayer;
	ePlayer = returnPlayerFromTrigger(getCurrentEntity());
	if(doesEntityExist(ePlayer)){
		printMsg(ePlayer,"Your Current Rank: ^5"+rank_getCurrent(ePlayer));
	}
}

void promoteRank()
//----------------------------------------------------------------------
// promots you in rank
//----------------------------------------------------------------------
{
	entity ePlayer;
	ePlayer = returnPlayerFromTrigger(getCurrentEntity());
	if(doesEntityExist(ePlayer)){
		string sRankOld;
		string sRankNew;
		
		sRankOld = rank_getCurrent(ePlayer);
		rank_promote(ePlayer);
		sRankNew = rank_getCurrent(ePlayer);
		
		string sMessage;
		sMessage = "You got promoted from ^3"+sRankOld+"^8 to ^5"+sRankNew;
		printMsg(ePlayer,sMessage);
	}
}

void demoteRank()
//----------------------------------------------------------------------
// demotes youi in rank
//----------------------------------------------------------------------
{
	entity ePlayer;
	ePlayer = returnPlayerFromTrigger(getCurrentEntity());
	if(doesEntityExist(ePlayer)){
		string sRankOld;
		string sRankNew;
		
		sRankOld = rank_getCurrent(ePlayer);
		rank_demote(ePlayer);
		sRankNew = rank_getCurrent(ePlayer);
		
		string sMessage;
		sMessage = "You got demoted from ^5"+sRankOld+"^8 to ^3"+sRankNew;
		printMsg(ePlayer,sMessage);
	}
}

void saveRank()
//----------------------------------------------------------------------
// saves rank of player into this map specific ini file (base/ini/prf_ranks.ini)
// 
// ! while you can read this file from other maps, you may only write to
// ! the ini file of the current map ONLY
//----------------------------------------------------------------------
{
	entity ePlayer;
	ePlayer = returnPlayerFromTrigger(getCurrentEntity());
	if(doesEntityExist(ePlayer)){
	
		string sPlayerRank;
		sPlayerRank = rank_getCurrent(ePlayer);
		
		//save rank and check if it worked
		if( rank_save(ePlayer) ){
			printMsg(ePlayer,"Your rank ^5"+sPlayerRank+"^8 was saved.");
		}else{
			printMsg(ePlayer,"Your rank ^5"+sPlayerRank+"^8 could not be saved!");
		}
	}
}

void retriveRank()
//----------------------------------------------------------------------
// reads saved rank of player of ini file (base/ini/prf_ranks.ini)
// and sets rank on player
//----------------------------------------------------------------------
{
	entity ePlayer;
	ePlayer = returnPlayerFromTrigger(getCurrentEntity());
	if(doesEntityExist(ePlayer)){
		if(rank_setupPlayer(ePlayer,"prf_ranks.ini")){
			printMsg(ePlayer,"Loaded your ^5rank^8 information from ini!");
		}else{
			printMsg(ePlayer,"Could not find your ^5rank^8 information in ini!");
		}
	}
}

void compareRank()
//----------------------------------------------------------------------
// compares two ranks
//----------------------------------------------------------------------
{
	entity ePlayer;
	ePlayer = returnPlayerFromTrigger(getCurrentEntity());
	if(doesEntityExist(ePlayer)){
	
		string sPlayerRank;
		sPlayerRank = rank_getCurrent(ePlayer);
		
		if(rank_equalOrAbove(rank_getCurrent(ePlayer),"Crewman")){
			printMsg(ePlayer,"Your rank ^5"+sPlayerRank+"^8 is above or equal to ^3Crewman");
		}else{
			printMsg(ePlayer,"Your rank ^3"+sPlayerRank+"^8 is below ^5Crewman");
		}
	}
}

void	coop_endLevel()
//----------------------------------------------------------------------
// ends the level
//----------------------------------------------------------------------
{
//load next map
	thread globalCoop_mission_completed("coop_circle");
}
